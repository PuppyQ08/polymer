SUBROUTINE STORE_FOURINDEX_ERI_PRIMITIVE
! CALCULATE AND STORE FOUR-CENTER TWO-ELECTRON ELECTRON REPULSION INTEGRALS
! USING THE OBARA-SAIKA RECURSION METHOD.  THIS SUBROUTINE WORKS CORRECTLY 
! ONLY WHEN HELIX EQUALS TO ZERO.

   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE GRADIENT
   USE FMT
   USE CONSTANTS
   
   IMPLICIT NONE
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
   INTEGER :: Q1,Q2,Q3
   INTEGER :: I,J,K,L
   INTEGER :: II,JJ,KK,LL,IJ,KL,M
   INTEGER :: I1,I2,I3,J1,J2,J3,IA,JA
   INTEGER :: IJT,KLT
   INTEGER :: NPSHELL_SQ
   INTEGER :: IJANG,SIJ,SKL,NIJ,IJKLANG,TS
   INTEGER :: DOWN1(300,9,3),DOWN2(300,9,3)
   INTEGER :: DOWN3(300,9),DOWN4(300,9),DOWN5(300,9),DOWNXYZ(300,9)
   INTEGER :: NKL(9),CGSI(300),CGSJ(300)
   INTEGER :: IJMAP(0:3,0:3,0:3,0:3,0:3,0:3)
   INTEGER :: KLANGINDX(300,9)
   INTEGER :: ICOUNT,ICASHECOUNT
   INTEGER(4),ALLOCATABLE :: ICASHE1(:),ICASHE2(:)
   INTEGER,ALLOCATABLE :: KLT_S(:),KLANG(:),CGSK(:,:),CGSL(:,:)
   INTEGER,ALLOCATABLE :: P2C(:)
   DOUBLE PRECISION :: ERI(0:9,300,300)
!  DOUBLE PRECISION :: Q1X,Q1C,Q1S,Q2X,Q2C,Q2S,Q3X,Q3C,Q3S
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,CX,CY,CZ,DX,DY,DZ,PX,PY,PZ,QX,QY,QZ
   DOUBLE PRECISION :: COMZ,C1,C2,LARGECOMZ,RHO_COMZ1,RHO_COMZ2,RHO,T,H
   DOUBLE PRECISION :: PI_AI(300),IJIJ,PMAX
   DOUBLE PRECISION :: PISUB,F1(0:9),F2(0:9),WX,WY,WZ,WI_PI(3),WI_QI(3)
   DOUBLE PRECISION :: DOWN1_C(300,9,3),DOWN2_C(300,9,3),DOWN4_C(300,9),DOWN5_C(300,9)
   DOUBLE PRECISION,ALLOCATABLE :: COMZ_S(:),PX_S(:),PY_S(:),PZ_S(:),OV(:),QI_CI(:,:)
   DOUBLE PRECISION,ALLOCATABLE :: KLKL(:)
   DOUBLE PRECISION,ALLOCATABLE :: W1(:,:),W2(:,:),W3(:,:)
   DOUBLE PRECISION :: CCIJ(300)
   DOUBLE PRECISION,ALLOCATABLE :: CCKL(:,:)
   DOUBLE PRECISION :: A
   DOUBLE PRECISION,ALLOCATABLE :: DCASHE(:)

   REWIND(31)

   CALL WARNING('CODE WILL ONLY WORK CORRECTLY FOR PRIMITIVE (UNCONTRACTED) GAUSSIANS')
   NPSHELL_SQ=NPSHELL**2
   ALLOCATE(COMZ_S(NPSHELL_SQ),PX_S(NPSHELL_SQ),PY_S(NPSHELL_SQ),PZ_S(NPSHELL_SQ),OV(NPSHELL_SQ),QI_CI(300,NPSHELL_SQ))
   ALLOCATE(KLT_S(NPSHELL_SQ),KLANG(NPSHELL_SQ),CGSK(300,NPSHELL_SQ),CGSL(300,NPSHELL_SQ),KLKL(NPSHELL_SQ))
   ALLOCATE(CCKL(300,NPSHELL_SQ),P2C(NPGS))
   ALLOCATE(ICASHE1(CASHESIZE),ICASHE2(CASHESIZE),DCASHE(CASHESIZE))

   WRITE(6,'(A)') 'CALCULATE FOUR-INDEX TWO-ELECTRON INTEGRALS ONCE AND STORE THEM ON DISK'
   IF (HELIX /= 0.0D0) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   IF ((NCGS >= 256).OR.(CEL1 >= 256).OR.(CEL2 >= 256)) CALL PABORT('TWO-ELECTRON INTEGRAL FILE IS TOO LARGE')
   DO I=1,NPGS
    K=0
    DO J=1,NCGS
     IF (CC(J,I) /= 0.0D0) THEN
      K=K+1
      P2C(I)=J
     ENDIF
    ENDDO
    IF (K == 0) THEN
     P2C(I)=1
    ELSE IF (K > 1) THEN
     CALL PABORT('GENERAL CONTRACTION IS NOT YET SUPPORTED FOR MP2 CALCULATIONS')
    ENDIF
   ENDDO
  
   ! PRELIMINARY
   ICOUNT=0
   ICASHECOUNT=0
   ICASHE1=-1
   ICASHE2=-1
   PISUB=2.0D0/DSQRT(PI)
   F1(0)=1.0D0/PISUB
   F1(1)=1.0D0/2.0D0/PISUB
   F1(2)=3.0D0/4.0D0/PISUB
   F1(3)=15.0D0/8.0D0/PISUB
   F1(4)=105.0D0/16.0D0/PISUB
   F1(5)=945.0D0/32.0D0/PISUB
   F1(6)=10395.0D0/64.0D0/PISUB
   F1(7)=135135.0D0/128.0D0/PISUB
   F1(8)=2027025.D0/256.0D0/PISUB
   F1(9)=34459425.0D0/512.0D0/PISUB
   F2(0)=-0.5D0
   F2(1)=-1.5D0
   F2(2)=-2.5D0
   F2(3)=-3.5D0
   F2(4)=-4.5D0
   F2(5)=-5.5D0
   F2(6)=-6.5D0
   F2(7)=-7.5D0
   F2(8)=-8.5D0
   F2(9)=-9.5D0
       
   ! PRELIMINARY FOR THE OBARA-SAIKA RECURSION FORMULA
   DOWN1=0
   DOWN2=0
   DOWN3=0
   DOWN4=0
   DOWN5=0
   DO IA=0,2
    DO JA=0,2
     KLT=IA*3+JA+1
     NKL(KLT)=0
     DO I1=0,IA
      DO I2=0,IA-I1
       DO I3=0,IA-I1-I2
        DO J1=0,JA
         DO J2=0,JA-J1
          DO J3=0,JA-J1-J2
           NKL(KLT)=NKL(KLT)+1
           KLANGINDX(NKL(KLT),KLT)=I1+I2+I3+J1+J2+J3
           IJMAP(I1,I2,I3,J1,J2,J3)=NKL(KLT)
           IF ((I1 >= 1).AND.(J1 >= 1)) THEN
            DOWN1(NKL(KLT),KLT,1)=IJMAP(I1-1,I2,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,1)=DFLOAT(I1)/2.0D0
            DOWN2(NKL(KLT),KLT,1)=IJMAP(I1,I2,I3,J1-1,J2,J3)
            DOWN2_C(NKL(KLT),KLT,1)=DFLOAT(J1)/2.0D0
           ELSE IF ((I1 >= 1).AND.(J1 == 0)) THEN
            DOWN1(NKL(KLT),KLT,1)=IJMAP(I1-1,I2,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,1)=DFLOAT(I1)/2.0D0
           ELSE IF ((J1 >= 1).AND.(I1 == 0)) THEN
            DOWN2(NKL(KLT),KLT,1)=IJMAP(I1,I2,I3,J1-1,J2,J3)
            DOWN2_C(NKL(KLT),KLT,1)=DFLOAT(J1)/2.0D0
           ENDIF
           IF ((I2 >= 1).AND.(J2 >= 1)) THEN
            DOWN1(NKL(KLT),KLT,2)=IJMAP(I1,I2-1,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,2)=DFLOAT(I2)/2.0D0
            DOWN2(NKL(KLT),KLT,2)=IJMAP(I1,I2,I3,J1,J2-1,J3)
            DOWN2_C(NKL(KLT),KLT,2)=DFLOAT(J2)/2.0D0
           ELSE IF ((I2 >= 1).AND.(J2 == 0)) THEN
            DOWN1(NKL(KLT),KLT,2)=IJMAP(I1,I2-1,I3,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,2)=DFLOAT(I2)/2.0D0
           ELSE IF ((J2 >= 1).AND.(I2 == 0)) THEN
            DOWN2(NKL(KLT),KLT,2)=IJMAP(I1,I2,I3,J1,J2-1,J3)
            DOWN2_C(NKL(KLT),KLT,2)=DFLOAT(J2)/2.0D0
           ENDIF
           IF ((I3 >= 1).AND.(J3 >= 1)) THEN
            DOWN1(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3-1,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,3)=DFLOAT(I3)/2.0D0
            DOWN2(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3,J1,J2,J3-1)
            DOWN2_C(NKL(KLT),KLT,3)=DFLOAT(J3)/2.0D0
           ELSE IF ((I3 >= 1).AND.(J3 == 0)) THEN
            DOWN1(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3-1,J1,J2,J3)
            DOWN1_C(NKL(KLT),KLT,3)=DFLOAT(I3)/2.0D0
           ELSE IF ((J3 >= 1).AND.(I3 == 0)) THEN
            DOWN2(NKL(KLT),KLT,3)=IJMAP(I1,I2,I3,J1,J2,J3-1)
            DOWN2_C(NKL(KLT),KLT,3)=DFLOAT(J3)/2.0D0
           ENDIF

           IF (I1 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=1
            DOWN3(NKL(KLT),KLT)=IJMAP(I1-1,I2,I3,J1,J2,J3)
            IF (I1 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1-2,I2,I3,J1,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(I1-1)/2.0D0
            ENDIF
            IF (J1 >= 1) THEN
             DOWN5(NKL(KLT),KLT)=IJMAP(I1-1,I2,I3,J1-1,J2,J3)
             DOWN5_C(NKL(KLT),KLT)=DFLOAT(J1)/2.0D0
            ENDIF
           ELSE IF (I2 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=2
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2-1,I3,J1,J2,J3)
            IF (I2 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2-2,I3,J1,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(I2-1)/2.0D0
            ENDIF
            IF (J2 >= 1) THEN
             DOWN5(NKL(KLT),KLT)=IJMAP(I1,I2-1,I3,J1,J2-1,J3)
             DOWN5_C(NKL(KLT),KLT)=DFLOAT(J2)/2.0D0
            ENDIF
           ELSE IF (I3 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=3
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3-1,J1,J2,J3)
            IF (I3 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3-2,J1,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(I3-1)/2.0D0
            ENDIF
            IF (J3 >= 1) THEN
             DOWN5(NKL(KLT),KLT)=IJMAP(I1,I2,I3-1,J1,J2,J3-1)
             DOWN5_C(NKL(KLT),KLT)=DFLOAT(J3)/2.0D0
            ENDIF
           ELSE IF (J1 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=1
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1-1,J2,J3)
            IF (J1 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1-2,J2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(J1-1)/2.0D0
            ENDIF
           ELSE IF (J2 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=2
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2-1,J3)
            IF (J2 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2-2,J3)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(J2-1)/2.0D0
            ENDIF
           ELSE IF (J3 >= 1) THEN
            DOWNXYZ(NKL(KLT),KLT)=3
            DOWN3(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2,J3-1)
            IF (J3 >= 2) THEN
             DOWN4(NKL(KLT),KLT)=IJMAP(I1,I2,I3,J1,J2,J3-2)
             DOWN4_C(NKL(KLT),KLT)=DFLOAT(J3-1)/2.0D0
            ENDIF
           ELSE
            DOWNXYZ(NKL(KLT),KLT)=0
           ENDIF
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   
   ! LOOP OVER UNIT CELL INDEX 1
   DO Q1=-REDUCED_CEL1,REDUCED_CEL1
!   Q1X=DFLOAT(Q1)*PERIOD
!   Q1C=DCOS(DFLOAT(Q1)*HELIX)
!   Q1S=DSIN(DFLOAT(Q1)*HELIX)
    ! LOOP OVER UNIT CELL INDEX 2
    DO Q2=-REDUCED_CEL1,REDUCED_CEL1
!    Q2X=DFLOAT(Q2)*PERIOD
!    Q2C=DCOS(DFLOAT(Q2)*HELIX)
!    Q2S=DSIN(DFLOAT(Q2)*HELIX)
     ! LOOP OVER KL SHELL
     DO K=1,NPSHELL
      KK=P_SHELL(K,0,0,0)
      CX=PGSX(KK)
      CY=PGSY(KK)
      CZ=PGSZ(KK)
      DO L=1,NPSHELL
       LL=P_SHELL(L,0,0,0)
!      DX=PGSX(LL)+Q2X
!      DY=PGSY(LL)*Q2C-PGSZ(LL)*Q2S
!      DZ=PGSY(LL)*Q2S+PGSZ(LL)*Q2C
       DX=PGSX(LL)+DFLOAT(Q2)*PERIOD
       DY=PGSY(LL)
       DZ=PGSZ(LL)
       COMZ=ZT(KK)+ZT(LL)
       PX=(CX*ZT(KK)+DX*ZT(LL))/COMZ
       PY=(CY*ZT(KK)+DY*ZT(LL))/COMZ
       PZ=(CZ*ZT(KK)+DZ*ZT(LL))/COMZ
       SKL=(K-1)*NPSHELL+L   ! SHELL INDEX
       COMZ_S(SKL)=COMZ      ! STORE ZETA+ZETA
       PX_S(SKL)=PX          ! STORE PX, PY, & PZ
       PY_S(SKL)=PY
       PZ_S(SKL)=PZ
       OV(SKL)=S_P(KK,LL,Q2)   ! STORE UNNORMALIZED OVERLAP INTEGRALS
       KLANG(SKL)=P_SHELL_ANG(K)+P_SHELL_ANG(L)   ! STORE SUM OF ANGULAR MOMENTA
       KLT_S(SKL)=P_SHELL_ANG(K)*3+P_SHELL_ANG(L)+1 ! STORE SHELL TYPE
       NIJ=0
       ! ELECTRON 2
       DO I1=0,P_SHELL_ANG(K)
        DO I2=0,P_SHELL_ANG(K)-I1
         DO I3=0,P_SHELL_ANG(K)-I1-I2
          DO J1=0,P_SHELL_ANG(L)
           DO J2=0,P_SHELL_ANG(L)-J1
            DO J3=0,P_SHELL_ANG(L)-J1-J2
             NIJ=NIJ+1
             CGSK(NIJ,SKL)=P2C(P_SHELL(K,I1,I2,I3))
             CGSL(NIJ,SKL)=P2C(P_SHELL(L,J1,J2,J3))
             CCKL(NIJ,SKL)=CC(P2C(P_SHELL(K,I1,I2,I3)),P_SHELL(K,I1,I2,I3))*CC(P2C(P_SHELL(L,J1,J2,J3)),P_SHELL(L,J1,J2,J3))
             IF (I1 >= 1) THEN
              QI_CI(NIJ,SKL)=PX-CX
             ELSE IF (I2 >= 1) THEN
              QI_CI(NIJ,SKL)=PY-CY
             ELSE IF (I3 >= 1) THEN
              QI_CI(NIJ,SKL)=PZ-CZ
             ELSE IF (J1 >= 1) THEN
              QI_CI(NIJ,SKL)=PX-DX
             ELSE IF (J2 >= 1) THEN
              QI_CI(NIJ,SKL)=PY-DY
             ELSE IF (J3 >= 1) THEN
              QI_CI(NIJ,SKL)=PZ-DZ
             ENDIF
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
       ENDDO
       IF (NIJ /= NKL(KLT_S(SKL))) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
      ENDDO
     ENDDO

     ! FIND THE MAXIMUM (KL|KL) VALUES FOR ALL SHELLS
     KLKL=0.0D0
     DO SKL=1,NPSHELL_SQ
      KLT=KLT_S(SKL)
      LARGECOMZ=2.0D0*COMZ_S(SKL)
      RHO=0.5D0*COMZ_S(SKL)
      IJKLANG=2*KLANG(SKL)
      C2=2.0D0*DSQRT(RHO/PI)*OV(SKL)**2
      IF (IJKLANG > 0) THEN
       DO M=0,IJKLANG
        ERI(M,1,1)=C2/DFLOAT(2*M+1)
       ENDDO
       DO IJ=1,NKL(KLT)
        IF (IJ == 1) THEN
         DO KL=2,NKL(KLT)
          DO M=0,IJKLANG-KLANGINDX(KL,KLT)
           ERI(M,KL,1)=QI_CI(KL,SKL)*ERI(M,DOWN3(KL,KLT),1)
           IF (DOWN4(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN4_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN4(KL,KLT),1)-0.5D0*ERI(M+1,DOWN4(KL,KLT),1))
           IF (DOWN5(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN5_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN5(KL,KLT),1)-0.5D0*ERI(M+1,DOWN5(KL,KLT),1))
          ENDDO
         ENDDO
        ELSE
         DO KL=1,NKL(KLT)
          DO M=0,IJKLANG-KLANGINDX(IJ,KLT)-KLANGINDX(KL,KLT)
           ERI(M,KL,IJ)=QI_CI(IJ,SKL)*ERI(M,KL,DOWN3(IJ,KLT))
           IF (DOWN4(IJ,KLT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN4_C(IJ,KLT)/COMZ_S(SKL)*(ERI(M,KL,DOWN4(IJ,KLT))-0.5D0*ERI(M+1,KL,DOWN4(IJ,KLT)))
           IF (DOWN5(IJ,KLT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN5_C(IJ,KLT)/COMZ_S(SKL)*(ERI(M,KL,DOWN5(IJ,KLT))-0.5D0*ERI(M+1,KL,DOWN5(IJ,KLT)))
           IF (DOWN1(KL,KLT,DOWNXYZ(IJ,KLT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN1_C(KL,KLT,DOWNXYZ(IJ,KLT))/LARGECOMZ &
                                                   *ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,KLT)),DOWN3(IJ,KLT))
           IF (DOWN2(KL,KLT,DOWNXYZ(IJ,KLT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN2_C(KL,KLT,DOWNXYZ(IJ,KLT))/LARGECOMZ &
                                                   *ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,KLT)),DOWN3(IJ,KLT))
          ENDDO
         ENDDO
        ENDIF
       ENDDO
       DO KL=2,NKL(KLT)
        A=DSQRT(ERI(0,KL,KL)*DABS(CCKL(KL,SKL)))
        IF (A > KLKL(SKL)) KLKL(SKL)=A
       ENDDO
      ELSE
       A=DSQRT(C2*DABS(CCKL(1,SKL)))
       IF (A > KLKL(SKL)) KLKL(SKL)=A
      ENDIF
     ENDDO

     ! LOOP OVER UNIT CELL INDEX 3
     DO Q3=0,CEL2
!     Q3X=DFLOAT(Q3)*PERIOD
!     Q3C=DCOS(DFLOAT(Q3)*HELIX)
!     Q3S=DSIN(DFLOAT(Q3)*HELIX)
      DO I=1,NPSHELL
       II=P_SHELL(I,0,0,0)
       AX=PGSX(II)
       AY=PGSY(II)
       AZ=PGSZ(II)
       DO J=1,NPSHELL
        JJ=P_SHELL(J,0,0,0)
!       BX=PGSX(JJ)+Q1X
!       BY=PGSY(JJ)*Q1C-PGSZ(JJ)*Q1S
!       BZ=PGSY(JJ)*Q1S+PGSZ(JJ)*Q1C
        BX=PGSX(JJ)+DFLOAT(Q1)*PERIOD
        BY=PGSY(JJ)
        BZ=PGSZ(JJ)
        COMZ=ZT(II)+ZT(JJ)
        PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ
        PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ
        PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ
        C1=S_P(II,JJ,Q1)*2.0D0/DSQRT(PI)
        SIJ=(I-1)*NPSHELL+J   ! SHELL INDEX
        IJT=P_SHELL_ANG(I)*3+P_SHELL_ANG(J)+1
        IJANG=P_SHELL_ANG(I)+P_SHELL_ANG(J)
        
        ! ELECTRON 1
        NIJ=0
        DO I1=0,P_SHELL_ANG(I)
         DO I2=0,P_SHELL_ANG(I)-I1
          DO I3=0,P_SHELL_ANG(I)-I1-I2
           DO J1=0,P_SHELL_ANG(J)
            DO J2=0,P_SHELL_ANG(J)-J1
             DO J3=0,P_SHELL_ANG(J)-J1-J2
              NIJ=NIJ+1
              CGSI(NIJ)=P2C(P_SHELL(I,I1,I2,I3))
              CGSJ(NIJ)=P2C(P_SHELL(J,J1,J2,J3))
              CCIJ(NIJ)=CC(P2C(P_SHELL(I,I1,I2,I3)),P_SHELL(I,I1,I2,I3))*CC(P2C(P_SHELL(J,J1,J2,J3)),P_SHELL(J,J1,J2,J3))
              IF (I1 >= 1) THEN
               PI_AI(NIJ)=PX-AX
              ELSE IF (I2 >= 1) THEN
               PI_AI(NIJ)=PY-AY
              ELSE IF (I3 >= 1) THEN
               PI_AI(NIJ)=PZ-AZ
              ELSE IF (J1 >= 1) THEN
               PI_AI(NIJ)=PX-BX
              ELSE IF (J2 >= 1) THEN
               PI_AI(NIJ)=PY-BY
              ELSE IF (J3 >= 1) THEN
               PI_AI(NIJ)=PZ-BZ
              ENDIF
             ENDDO
            ENDDO
           ENDDO
          ENDDO
         ENDDO
        ENDDO
        IF (NIJ /= NKL(KLT_S(SIJ))) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
     
        ! FIND THE MAXIMUM (IJ|IJ) VALUE FOR THE CURRENT SHELL
        IJIJ=0.0D0
        LARGECOMZ=2.0D0*COMZ
        RHO=0.5D0*COMZ
        IJKLANG=2*IJANG
        C2=2.0D0*DSQRT(RHO/PI)*S_P(II,JJ,Q1)**2
        IF (IJKLANG > 0) THEN
         DO M=0,IJKLANG
          ERI(M,1,1)=C2/DFLOAT(2*M+1)
         ENDDO
         DO IJ=1,NIJ
          IF (IJ == 1) THEN
           DO KL=2,NIJ
            DO M=0,IJKLANG-KLANGINDX(KL,IJT)
             ERI(M,KL,1)=QI_CI(KL,SIJ)*ERI(M,DOWN3(KL,IJT),1)
             IF (DOWN4(KL,IJT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN4_C(KL,IJT)/COMZ*(ERI(M,DOWN4(KL,IJT),1)-0.5D0*ERI(M+1,DOWN4(KL,IJT),1))
             IF (DOWN5(KL,IJT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN5_C(KL,IJT)/COMZ*(ERI(M,DOWN5(KL,IJT),1)-0.5D0*ERI(M+1,DOWN5(KL,IJT),1))
            ENDDO
           ENDDO
          ELSE
           DO KL=1,NIJ
            DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,IJT)
             ERI(M,KL,IJ)=QI_CI(IJ,SIJ)*ERI(M,KL,DOWN3(IJ,IJT))
             IF (DOWN4(IJ,IJT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-0.5D0*ERI(M+1,KL,DOWN4(IJ,IJT)))
             IF (DOWN5(IJ,IJT) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ) &
           +DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-0.5D0*ERI(M+1,KL,DOWN5(IJ,IJT)))
             IF (DOWN1(KL,IJT,DOWNXYZ(IJ,IJT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN1_C(KL,IJT,DOWNXYZ(IJ,IJT))/LARGECOMZ &
                                                     *ERI(M+1,DOWN1(KL,IJT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
             IF (DOWN2(KL,IJT,DOWNXYZ(IJ,IJT)) /= 0) ERI(M,KL,IJ)=ERI(M,KL,IJ)+DOWN2_C(KL,IJT,DOWNXYZ(IJ,IJT))/LARGECOMZ &
                                                     *ERI(M+1,DOWN2(KL,IJT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
            ENDDO
           ENDDO
          ENDIF
         ENDDO
         DO IJ=2,NIJ
          A=DSQRT(ERI(0,IJ,IJ)*DABS(CCIJ(IJ)))
          IF (A > IJIJ) IJIJ=A
         ENDDO
        ELSE
         A=DSQRT(C2*DABS(CCIJ(1)))
         IF (A > IJIJ) IJIJ=A
        ENDIF
        
        ! LOOP OVER KL SHELL
        DO SKL=1,NPSHELL_SQ
         IF (IJIJ*KLKL(SKL) < DOPTN(12)) CYCLE
         LARGECOMZ=COMZ+COMZ_S(SKL)
         RHO_COMZ1=COMZ_S(SKL)/LARGECOMZ
         RHO_COMZ2=COMZ/LARGECOMZ
         RHO=COMZ*RHO_COMZ1
         IJKLANG=IJANG+KLANG(SKL)
         C2=C1*DSQRT(RHO)*OV(SKL)
         KLT=KLT_S(SKL)
         IF (IJKLANG > 0) THEN
!         QX=PX_S(SKL)+Q3X
!         QY=PY_S(SKL)*Q3C-PZ_S(SKL)*Q3S
!         QZ=PY_S(SKL)*Q3S+PZ_S(SKL)*Q3C
          QX=PX_S(SKL)+DFLOAT(Q3)*PERIOD
          QY=PY_S(SKL)
          QZ=PZ_S(SKL)
          T=RHO*((QX-PX)**2+(QY-PY)**2+(QZ-PZ)**2)
          DO M=0,IJKLANG
           IF (T < TF(M)) THEN
            TS=NINT(T*20.0D0)
            H=0.05D0*DFLOAT(TS)-T
            ERI(M,1,1)=((((((IGAMMA(TS,M+6)*H*0.166666666666667D0+IGAMMA(TS,M+5))*H*0.2D0+IGAMMA(TS,M+4))*H*0.25D0+ &
            IGAMMA(TS,M+3))*H*0.333333333333333D0+IGAMMA(TS,M+2))*H*0.5D0+IGAMMA(TS,M+1))*H+IGAMMA(TS,M))*C2
           ELSE
            ERI(M,1,1)=C2*F1(M)*DEXP(F2(M)*DLOG(T))
           ENDIF
          ENDDO
          WX=(PX*COMZ+QX*COMZ_S(SKL))/LARGECOMZ
          WY=(PY*COMZ+QY*COMZ_S(SKL))/LARGECOMZ
          WZ=(PZ*COMZ+QZ*COMZ_S(SKL))/LARGECOMZ
          WI_PI(1)=WX-PX
          WI_PI(2)=WY-PY
          WI_PI(3)=WZ-PZ
          WI_QI(1)=WX-QX
          WI_QI(2)=WY-QY
          WI_QI(3)=WZ-QZ
          IF (NKL(KLT) /= 1) THEN
           DO KL=2,NKL(KLT)
            DO M=0,IJKLANG-KLANGINDX(KL,KLT)
             ERI(M,KL,1)=QI_CI(KL,SKL)*ERI(M,DOWN3(KL,KLT),1)+WI_QI(DOWNXYZ(KL,KLT))*ERI(M+1,DOWN3(KL,KLT),1)
             IF (DOWN4(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN4_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN4(KL,KLT),1)-RHO_COMZ2*ERI(M+1,DOWN4(KL,KLT),1))
             IF (DOWN5(KL,KLT) /= 0) ERI(M,KL,1)=ERI(M,KL,1) &
           +DOWN5_C(KL,KLT)/COMZ_S(SKL)*(ERI(M,DOWN5(KL,KLT),1)-RHO_COMZ2*ERI(M+1,DOWN5(KL,KLT),1))
            ENDDO
           ENDDO
          ENDIF
          IF (NIJ /= 1) THEN
           DO IJ=2,NIJ
            IF ((DOWN4(IJ,IJT) == 0).AND.(DOWN5(IJ,IJT) == 0)) THEN
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ELSE IF (DOWN5(IJ,IJT) == 0) THEN
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT)))
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN4_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ELSE IF (DOWN4(IJ,IJT) == 0) THEN
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT)))
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))) &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                + DOWN5_C(IJ,IJT)/COMZ*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))) &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ELSE
             DO KL=1,NKL(KLT)
              IF ((DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0).AND.(DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0)) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ
               ENDDO
              ELSE IF (DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ &
                + DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE IF (DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)) == 0) THEN
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))/LARGECOMZ*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT))
               ENDDO
              ELSE
               DO M=0,IJKLANG-KLANGINDX(IJ,IJT)-KLANGINDX(KL,KLT)
                ERI(M,KL,IJ)=PI_AI(IJ)*ERI(M,KL,DOWN3(IJ,IJT))+WI_PI(DOWNXYZ(IJ,IJT))*ERI(M+1,KL,DOWN3(IJ,IJT)) &
                +(DOWN4_C(IJ,IJT)*(ERI(M,KL,DOWN4(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN4(IJ,IJT))) &
                + DOWN5_C(IJ,IJT)*(ERI(M,KL,DOWN5(IJ,IJT))-RHO_COMZ1*ERI(M+1,KL,DOWN5(IJ,IJT))))/COMZ &
                +(DOWN1_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN1(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)) &
                + DOWN2_C(KL,KLT,DOWNXYZ(IJ,IJT))*ERI(M+1,DOWN2(KL,KLT,DOWNXYZ(IJ,IJT)),DOWN3(IJ,IJT)))/LARGECOMZ
               ENDDO
              ENDIF
             ENDDO
            ENDIF
           ENDDO
          ENDIF
          DO IJ=1,NIJ
           DO KL=1,NKL(KLT)
            IF (DABS(ERI(0,KL,IJ)*CCIJ(IJ)*CCKL(KL,SKL)) > DOPTN(12)) THEN
             ICOUNT=ICOUNT+1
             IF (DFLOAT(ICOUNT)*16.0D0 > DOPTN(26)*1000000.0D0) CALL PABORT('THE SIZE OF THE INTEGRAL FILE EXCEEDED MAXDISK')
             ICASHECOUNT=ICASHECOUNT+1
             ICASHE1(ICASHECOUNT)=((Q1+CEL1)*256+(Q2+CEL1))*256+(Q3+CEL2)
             ICASHE2(ICASHECOUNT)=((CGSI(IJ)*256+CGSJ(IJ))*256+CGSK(KL,SKL))*256+CGSL(KL,SKL)
             DCASHE(ICASHECOUNT) =ERI(0,KL,IJ)*CCIJ(IJ)*CCKL(KL,SKL)
             IF (ICASHECOUNT == CASHESIZE) THEN
              WRITE(31) ICASHE1,ICASHE2,DCASHE
              ICASHECOUNT=0
              ICASHE1=-1
              ICASHE2=-1
             ENDIF
            ENDIF
           ENDDO
          ENDDO
         ELSE
!         QX=PX_S(SKL)+Q3X
!         QY=PY_S(SKL)*Q3C-PZ_S(SKL)*Q3S
!         QZ=PY_S(SKL)*Q3S+PZ_S(SKL)*Q3C
          QX=PX_S(SKL)+DFLOAT(Q3)*PERIOD
          QY=PY_S(SKL)
          QZ=PZ_S(SKL)
          T=RHO*((QX-PX)**2+(QY-PY)**2+(QZ-PZ)**2)
          IF (T < TF(0)) THEN
           TS=NINT(T*20.0D0)
           H=0.05D0*DFLOAT(TS)-T
           ERI(0,1,1)=((((((IGAMMA(TS,6)*H*0.166666666666667D0+IGAMMA(TS,5))*H*0.2D0+IGAMMA(TS,4))*H*0.25D0+ &
           IGAMMA(TS,3))*H*0.333333333333333D0+IGAMMA(TS,2))*H*0.5D0+IGAMMA(TS,1))*H+IGAMMA(TS,0))*C2
          ELSE
           ERI(0,1,1)=C2*F1(0)*DEXP(-0.5D0*DLOG(T))
          ENDIF
          IF (DABS(ERI(0,1,1)*CCIJ(1)*CCKL(1,SKL)) > DOPTN(12)) THEN
           ICOUNT=ICOUNT+1
           IF (DFLOAT(ICOUNT)*16.0D0 > DOPTN(26)*1000000.0D0) CALL PABORT('THE SIZE OF THE INTEGRAL FILE EXCEEDED MAXDISK')
           ICASHECOUNT=ICASHECOUNT+1
           ICASHE1(ICASHECOUNT)=((Q1+CEL1)*256+(Q2+CEL1))*256+(Q3+CEL2)
           ICASHE2(ICASHECOUNT)=((CGSI(1)*256+CGSJ(1))*256+CGSK(1,SKL))*256+CGSL(1,SKL)
           DCASHE(ICASHECOUNT) =ERI(0,1,1)*CCIJ(1)*CCKL(1,SKL)
           IF (ICASHECOUNT == CASHESIZE) THEN
            WRITE(31) ICASHE1,ICASHE2,DCASHE
            ICASHECOUNT=0
            ICASHE1=-1
            ICASHE2=-1
           ENDIF
          ENDIF
         ENDIF
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   IF (ICASHECOUNT /= 0) WRITE(31) ICASHE1,ICASHE2,DCASHE

   WRITE(6,'(I9,A,F7.3,A)') ICOUNT,' ERIS (', &
           DFLOAT(ICOUNT)/DFLOAT((CEL1*2+1)**2)/DFLOAT(CEL2+1)/DFLOAT(NCGS**4)*100.0,'% OF TOTAL ERIS) HAVE BEEN STORED'
   IF (DFLOAT(ICOUNT)*16.0/1000000.0 > 1.0) THEN
    WRITE(6,'(F9.1,A)') DFLOAT(ICOUNT)*16.0/1000000.0,' MB DISK SPACE HAS BEEN USED'
   ELSE
    WRITE(6,'(F9.1,A)') DFLOAT(ICOUNT)*16.0/1000.0,' KB DISK SPACE HAS BEEN USED'
   ENDIF
   DEALLOCATE(COMZ_S,PX_S,PY_S,PZ_S,OV,QI_CI)
   DEALLOCATE(KLT_S,KLANG,CGSK,CGSL,KLKL)
   DEALLOCATE(CCKL,P2C)
   DEALLOCATE(ICASHE1,ICASHE2,DCASHE)
   
   RETURN
END SUBROUTINE
