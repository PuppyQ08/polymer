SUBROUTINE POLARIZABILITY(CXYZ)
! DIRECT DAVIDSON-POPLE LIKE CPKS DYNAMIC POLARIZABILITY CALCULATION
! INITIAL TRIAL VECTOR: X_AI(0)   = -<A|X,Y,Z|I>/(E_A-E_I-OMEGA)
!                       Y_AI(0)   = -<A|X,Y,Z|I>/(E_A-E_I+OMEGA)
! NEW TRIAL VECTOR:     X_AI(N+1) = [ (AI|||JB)X_BJ(N)+(AI|||BJ)Y_BJ ]/(E_A-E_I-OMEGA)
!                       Y_AI(N+1) = [ (IA|||JB)X_BJ(N)+(IA|||BJ)Y_BJ ]/(E_A-E_I+OMEGA)
! SEE E.R.DAVIDSON,J.COMPUT.PHYS.17,87(1975);J.OLSEN,H.JORGEN,AA.JENSEN,AND P.JORGENSEN,J.COMPUT.PHYS.74,265(1988).
! SEE R.BAUERNSCHMITT AND R.AHLRICHS,CHEM.PHYS.LETT.256,454(1996);C.JAMORSKI,M.CASIDA,AND D.R.SALAHUB,J.CHEM.PHYS.104,5134(1996).
! SEE M.G.VRACKO AND M.ZAIDER,INT.J.QUANTUM.CHEM.43,321(1992);47,119(1993);
! M.VRACKO,B.CHAMPAGNE,D.H.MOSLEY,AND J.-M.ANDRE,J.CHEM.PHYS.102,6831(1995).

   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT
   USE CIS

   IMPLICIT NONE
   REAL :: ICPUS,ICPUE
   CHARACTER(1) :: CXYZ
   INTEGER :: IXYZ
   INTEGER :: NMAX
   DOUBLE PRECISION :: WEIGHT                                ! GAUSS-CHEVYSHEV WEIGHT
   DOUBLE PRECISION :: C6                                    ! VAN DER WAALS C6
   DOUBLE PRECISION,ALLOCATABLE :: POLAR(:)
   DOUBLE COMPLEX,ALLOCATABLE :: R(:,:)                      ! RHS VECTOR
   DOUBLE COMPLEX,ALLOCATABLE :: X(:,:),Y(:,:)               ! TRIAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: XP(:,:),YP(:,:)             ! PRODUCT VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: NX(:,:),NY(:,:)             ! NEW TRIAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: RX(:,:),RY(:,:)             ! RESIDUAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: P_AX(:,:,:),P_AY(:,:,:)     ! TRIAL DENSITY MATRIX FOR ONE TRIAL VECTOR
   DOUBLE COMPLEX,ALLOCATABLE :: P_BX(:,:,:),P_BY(:,:,:)     ! TRIAL DENSITY MATRIX FOR ONE TRIAL VECTOR
   DOUBLE COMPLEX,ALLOCATABLE :: A_X(:,:,:),A_Y(:,:,:),B_X(:,:,:),B_Y(:,:,:) ! PRODUCT DENSITY MATRICES
   DOUBLE COMPLEX,ALLOCATABLE :: PXP(:,:),PXR(:),C(:)        ! SUBSPACE REPRESENTATION
   DOUBLE COMPLEX,ALLOCATABLE :: PHASE(:)                    ! PHASE
   DOUBLE COMPLEX,ALLOCATABLE :: WX(:,:),WY(:,:)             ! WORK SPACE
   DOUBLE PRECISION,ALLOCATABLE :: WE(:)                     ! WORK SPACE
   DOUBLE COMPLEX :: T                                       ! UNIMPORTANT
   DOUBLE PRECISION :: S                                     ! UNIMPORTANT
   DOUBLE PRECISION :: POMEGA                                ! EXTERNAL FREQUENCY = DABS(DOPTN(87))
   DOUBLE COMPLEX :: ALPHA                                   ! POLARIZABILITY
   DOUBLE PRECISION :: PREV
   LOGICAL :: IMAGINARY                                      ! .TRUE. WHEN IMAGINARY FREQ PERTURBATION FOR C6
   LOGICAL,ALLOCATABLE :: LDONE(:)                           ! .TRUE. WHEN PRODUCT FORMATION HAS ALREADY BEEN DONE
   INTEGER :: I,J,K,L,M,N,Q,I1
   INTEGER :: MOI,MOJ,MOA,MOB
   INTEGER :: ITR

   WRITE(6,'(A)') ' '
   IF (KVC /= 0) CALL PABORT('POLARIZABILITY FOR POLYMERS NYT')
   IF (CXYZ == 'X') THEN
    IXYZ=1
   ELSE IF (CXYZ == 'Y') THEN
    IXYZ=2
   ELSE IF (CXYZ == 'Z') THEN
    IXYZ=3
   ELSE
    CALL PABORT('BUG TRAP')
   ENDIF
   IF (IOPTN(88) == 1) THEN
    WRITE(6,'(A)') 'POPLE-TYPE TRIAL VECTORS'
    NMAX = 1
   ELSE IF (IOPTN(88) == 2) THEN
    WRITE(6,'(A)') 'POPLE-DAVIDSON-TYPE TRIAL VECTORS'
    NMAX = 1
   ELSE IF (IOPTN(88) == 3) THEN
    WRITE(6,'(A)') 'GAUSS-CHEVYSHEV POPLE-TYPE TRIAL VECTORS'
    NMAX = 10
   ELSE IF (IOPTN(88) == 4) THEN
    WRITE(6,'(A)') 'GAUSS-CHEVYSHEV POPLE-DAVIDSON-TYPE TRIAL VECTORS'
    NMAX = 10
   ELSE
    CALL PABORT('UNKNOWN ALGORITHM')
   ENDIF
   IF (LOPTN(92)) CALL WARNING('SUM-OVER-STATE APPROXIMATION IS INVOKED')

   NOV=(IOCC-ICORE)*(IALLMAX-IOCC-IVIRTCORE)
   MAXTRIALS=MIN(INT(DOPTN(26)*1000000.0D0/(DFLOAT(NOV*MAX(1,2*KVC))*32.0D0)),NOV*MAX(1,2*KVC))
   WRITE(6,'(A,I3)') 'MAXIMUM NUMBER OF TRIAL VECTORS = ',MAXTRIALS

   ALLOCATE(POLAR(NMAX))
   ALLOCATE(R(NOV,-KVC:KVC))
   ALLOCATE(X(NOV,-KVC:KVC) ,Y(NOV,-KVC:KVC))
   ALLOCATE(XP(NOV,-KVC:KVC),YP(NOV,-KVC:KVC))
   ALLOCATE(NX(NOV,-KVC:KVC),NY(NOV,-KVC:KVC))
   ALLOCATE(LDONE(MAXTRIALS))
   ALLOCATE(PHASE(-CEL1*KVC:CEL1*KVC))

   ! PRECALCULATE PHASE FACTORS
   DO I=-CEL1*KVC,CEL1*KVC
    IF (KVC == 0) THEN
     PHASE(I)=DCMPLX(1.0D0,0.0D0)
    ELSE
     PHASE(I)=DCMPLX(DCOS(DFLOAT(I)/DFLOAT(KVC)*PI),DSIN(DFLOAT(I)/DFLOAT(KVC)*PI))
    ENDIF
   ENDDO

   ! CALCULATE MO MOMENT VECTOR AKA RIGHT-HAND SIDE (WITHOUT SIGN)
   DO K=-KVC,MAX(0,KVC-1)
    L=0
    DO MOI=ICORE+1,IOCC
     DO MOA=IOCC+1,IALL(K)-IVIRTCORE
      L=L+1
      R(L,K)=DCMPLX(0.0D0,0.0D0)
      DO I=1,NCGS
       DO J=1,NCGS
        DO Q=-CEL1,CEL1
         R(L,K)=R(L,K)+DCONJG(CO(I,MOA,K))*CO(J,MOI,K)*M_C(I,J,Q,IXYZ)*PHASE(K*Q)
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   IF (IOPTN(9) == 3) THEN
    WRITE(6,'(A,I3)') 'RIGHT-HAND-SIDE VECTOR (SIGN REVERSED)'
    CALL DUMP8(R,NOV,KVC)
   ENDIF
   
   ! /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\
   ! GAUSS-CHEVYSHEV QUADRATURE FOR VAN DER WAALS DISPERSION C6 COEFFICIENTS
   DO I1 = 1, NMAX
    IF (NMAX > 1) POMEGA = 1.0D0 / DTAN((2.0D0*DFLOAT(I1)-1.0D0) / (DFLOAT(NMAX) * 4.0D0) * PI)
   ! \/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/

   IMAGINARY = .FALSE.
   IF (NMAX > 1) THEN
    IMAGINARY = .TRUE.
    WRITE(6,*)
    WRITE(6,'(A,A1,A1,A,E10.3,A)') 'DYNAMIC ',CXYZ,CXYZ,' POLARIZABILITY AT IMAGINARY ',POMEGA,' A.U. WILL BE COMPUTED'
   ELSE IF (DOPTN(87) == 0.0D0) THEN
    POMEGA = 0.0D0
    WRITE(6,*)
    WRITE(6,'(A,A1,A1,A)') 'STATIC ',CXYZ,CXYZ,' POLARIZABILITY WILL BE COMPUTED'
   ELSE IF (DOPTN(87) > 0.0D0) THEN
    POMEGA = DOPTN(87)
    WRITE(6,*)
    WRITE(6,'(A,A1,A1,A,E10.3,A)') 'DYNAMIC ',CXYZ,CXYZ,' POLARIZABILITY AT ',POMEGA,' A.U. WILL BE COMPUTED'
   ELSE
    IMAGINARY = .TRUE.
    POMEGA = -DOPTN(87) 
    WRITE(6,*)
    WRITE(6,'(A,A1,A1,A,E10.3,A)') 'DYNAMIC ',CXYZ,CXYZ,' POLARIZABILITY AT IMAGINARY ',POMEGA,' A.U. WILL BE COMPUTED'
   ENDIF

   ! CREATE THE INITIAL TRIAL VECTOR
   DO K=-KVC,MAX(0,KVC-1)
    L=0
    DO MOI=ICORE+1,IOCC
     DO MOA=IOCC+1,IALL(K)-IVIRTCORE
      L=L+1
      IF (IMAGINARY) THEN
       X(L,K)=-R(L,K)/DCMPLX(EPSILON(MOA,K)-EPSILON(MOI,K),-POMEGA)
       Y(L,K)=-DCONJG(R(L,K))/DCMPLX(EPSILON(MOA,K)-EPSILON(MOI,K),+POMEGA)
      ELSE
       X(L,K)=-R(L,K)/(EPSILON(MOA,K)-EPSILON(MOI,K)-POMEGA)
       Y(L,K)=-DCONJG(R(L,K))/(EPSILON(MOA,K)-EPSILON(MOI,K)+POMEGA)
      ENDIF
     ENDDO
    ENDDO
   ENDDO
   REWIND(36)
   WRITE(36) X,Y
   NTRIALS=1

   LDONE(1:MAXTRIALS)=.FALSE.
   ITR=0
   WRITE(6,'(A)') 'COUPLED PERTURBED HF/KS ITERATION'
   WRITE(6,'(A)') '-------------------------------------------------------------------'
   WRITE(6,'(A)') 'ITR    VECTORDEV                   POLAR                  CPU / SEC'

   PREV=1.0D99
   ! MAIN LOOP
   DO
    ITR=ITR+1
    REWIND(36)
    REWIND(37)

    ! LOOP OVER TRIAL VECTORS

    ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1),P_BX(NCGS,NCGS,-CEL1:CEL1),P_AY(NCGS,NCGS,-CEL1:CEL1),P_BY(NCGS,NCGS,-CEL1:CEL1))
    ALLOCATE(A_X(NCGS,NCGS,-CEL1:CEL1),A_Y(NCGS,NCGS,-CEL1:CEL1),B_X(NCGS,NCGS,-CEL1:CEL1),B_Y(NCGS,NCGS,-CEL1:CEL1))

    CALL PCPU_TIME(ICPUS)
    DO N=1,NTRIALS

     ! IF THE PRODUCT FORMATION HAS ALREADY BEEN DONE, JUST PROCEED
     IF (LDONE(N)) THEN
      READ(36) X,Y
      READ(37) X,Y
     ELSE
      LDONE(N)=.TRUE.
      ! TRANSFORM TRIAL VECTORS TO TRIAL DENSITY MATRICES
      READ(36) X,Y
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'X-COMPONENT OF THE TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(X,NOV,KVC)
       WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(Y,NOV,KVC)
      ENDIF
      DO I=1,NCGS
       DO J=1,NCGS
        DO Q=-CEL1,CEL1
         P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         P_BY(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         P_BX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         P_AY(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         DO K=-KVC,MAX(0,KVC-1)
          L=0
          DO MOI=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(K)-IVIRTCORE
            L=L+1
            P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*X(L,K)*PHASE(K*Q)
            P_BY(I,J,Q)=P_BY(I,J,Q)+DCONJG(CO(I,MOA,K))*CO(J,MOI,K)*Y(L,K)*PHASE(K*Q)
            P_BX(I,J,Q)=P_BX(I,J,Q)+CO(I,MOA,K)*DCONJG(CO(J,MOI,K))*X(L,K)*PHASE(-K*Q)
            P_AY(I,J,Q)=P_AY(I,J,Q)+CO(I,MOI,K)*DCONJG(CO(J,MOA,K))*Y(L,K)*PHASE(-K*Q)
           ENDDO
          ENDDO
          IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
         ENDDO
         P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
         P_BY(I,J,Q)=P_BY(I,J,Q)/DFLOAT(MAX(1,2*KVC))
         P_BX(I,J,Q)=P_BX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
         P_AY(I,J,Q)=P_AY(I,J,Q)/DFLOAT(MAX(1,2*KVC))
        ENDDO
       ENDDO
      ENDDO
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'AX-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_AX,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'BY-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_BY,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'BX-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_BX,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'AY-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_AY,NCGS,CEL1)
      ENDIF

      ! ZERO SCRATCH PRODUCT MATRICES
      A_X(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      B_Y(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      B_X(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      A_Y(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)

      IF (.NOT.LOPTN(92)) THEN

        ! CONTRACT TRIAL DENSITY MATRICES WITH OEP KERNEL
        IF (DOPTN(84) /= 0.0D0) THEN
         CALL CONTRACT_OEP(A_X,P_AX,NCGS,CEL1,1)
         CALL CONTRACT_OEP(B_Y,P_BY,NCGS,CEL1,1)
         CALL CONTRACT_OEP(B_X,P_BX,NCGS,CEL1,1)
         CALL CONTRACT_OEP(A_Y,P_AY,NCGS,CEL1,1)
        ENDIF

        ! CONTRACT TRIAL DENSITY MATRICES WITH TWO-ELECTRON INTEGRALS
        IF (LOPTN(25)) THEN
         CALL DIRECT_CONTRACT_FOURINDEX_ERI(.TRUE.,A_X,P_AX,B_Y,P_BY,B_X,P_BX,A_Y,P_AY,NCGS,CEL1,1)
        ELSE
         CALL CONTRACT_FOURINDEX_ERI(A_X,P_AX,NCGS,CEL1,1)
         CALL CONTRACT_FOURINDEX_ERI(B_Y,P_BY,NCGS,CEL1,1)
         CALL CONTRACT_FOURINDEX_ERI(B_X,P_BX,NCGS,CEL1,1)
         CALL CONTRACT_FOURINDEX_ERI(A_Y,P_AY,NCGS,CEL1,1)
        ENDIF

        ! CONTRACT TRIAL DENSITY MATRICES WITH EXCHANGE-CORRELATION INTEGRALS
        IF (LDFT) THEN
         D2F1=0.0D0
         IF (LGC) THEN
          D1F2=0.0D0
          D2F2=0.0D0
          D2F3=0.0D0
         ENDIF
        ENDIF
        IF (DOPTN(20) /= 0.0D0) CALL SLATER_EXCHANGE(1)
        IF (DOPTN(21) /= 0.0D0) CALL VOSKO_WILK_NUSAIR_CORRELATION(1)
        IF (DOPTN(22) /= 0.0D0) CALL BECKE88_EXCHANGE(1)
        IF (DOPTN(23) /= 0.0D0) CALL LEE_YANG_PARR_CORRELATION(1)
        IF (LDFT.AND.(.NOT.(LGC))) THEN
         CALL TRIAL_ELECTRON_DENSITY(P_AX,NCGS,CEL1)
         CALL CONTRACT_LOCAL_XC(A_X,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY(P_BY,NCGS,CEL1)
         CALL CONTRACT_LOCAL_XC(B_Y,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY(P_BX,NCGS,CEL1)
         CALL CONTRACT_LOCAL_XC(B_X,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY(P_AY,NCGS,CEL1)
         CALL CONTRACT_LOCAL_XC(A_Y,NCGS,CEL1)
        ELSE IF (LDFT.AND.LGC) THEN
         CALL TRIAL_ELECTRON_DENSITY(P_AX,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_AX,NCGS,CEL1)
         CALL CONTRACT_GC_XC(A_X,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY(P_BY,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_BY,NCGS,CEL1)
         CALL CONTRACT_GC_XC(B_Y,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY(P_BX,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_BX,NCGS,CEL1)
         CALL CONTRACT_GC_XC(B_X,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY(P_AY,NCGS,CEL1)
         CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_AY,NCGS,CEL1)
         CALL CONTRACT_GC_XC(A_Y,NCGS,CEL1)
        ENDIF

      ENDIF
            
      ! SET B_X, B_Y AND A_Y ZERO FOR CIS CALCULATIONS
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'A TIMES X FOR ROOT ',N
       CALL DUMP9(A_X,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'B TIMES Y FOR ROOT ',N
       CALL DUMP9(B_Y,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'B TIMES X FOR ROOT ',N
       CALL DUMP9(B_X,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'A TIMES Y FOR ROOT ',N
       CALL DUMP9(A_Y,NCGS,CEL1)
      ENDIF

      ! BACK-TRANSFORM PRODUCT DENSITY MATRICES TO PRODUCT VECTORS
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         XP(L,K)=DCMPLX(0.0D0,0.0D0)
         YP(L,K)=DCMPLX(0.0D0,0.0D0)
         DO I=1,NCGS
          DO J=1,NCGS
           DO Q=-CEL1,CEL1
            XP(L,K)=XP(L,K)+DCONJG(CO(I,MOA,K))*CO(J,MOI,K)*(A_X(I,J,Q)+B_Y(I,J,Q))*PHASE(K*Q)
            YP(L,K)=YP(L,K)+CO(I,MOA,K)*DCONJG(CO(J,MOI,K))*(B_X(I,J,Q)+A_Y(I,J,Q))*PHASE(-K*Q)
           ENDDO
          ENDDO
         ENDDO
         ! ADD ONE-ELECTRON ENERGY DIFFERENCES
         IF (IMAGINARY) THEN
          XP(L,K)=XP(L,K)+DCMPLX(EPSILON(MOA,K)-EPSILON(MOI,K),-POMEGA)*X(L,K)
          YP(L,K)=YP(L,K)+DCMPLX(EPSILON(MOA,K)-EPSILON(MOI,K),+POMEGA)*Y(L,K)
         ELSE
          XP(L,K)=XP(L,K)+(EPSILON(MOA,K)-EPSILON(MOI,K)-POMEGA)*X(L,K)
          YP(L,K)=YP(L,K)+(EPSILON(MOA,K)-EPSILON(MOI,K)+POMEGA)*Y(L,K)
         ENDIF
        ENDDO
       ENDDO
      ENDDO

      ! STORE THE PRODUCT VECTORS
      WRITE(37) XP,YP
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'X-COMPONENT OF THE PRODUCT VECTOR FOR ROOT ',N
       CALL DUMP8(XP,NOV,KVC)
       WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE PRODUCT VECTOR FOR ROOT ',N
       CALL DUMP8(YP,NOV,KVC)
      ENDIF

     ENDIF
    ENDDO

    DEALLOCATE(P_AX,P_BX,P_AY,P_BY,A_X,B_X,A_Y,B_Y)
    ALLOCATE(PXP(NTRIALS,NTRIALS),PXR(NTRIALS),C(NTRIALS))
    ALLOCATE(WX(NTRIALS,NTRIALS),WY(NTRIALS,NTRIALS),WE(NTRIALS))

    ! FORM PXP AND PXR MATRICES IN THE SUBSPACE SPANNED BY THE TRIAL VECTORS
    ! PXP(J,I) = PRODUCT_VECTOR(J)*PRODUCT_VECTOR(I)
    ! PXR(J)   = 2*PRODUCT_VECTOR(J)*RIGHT_HAND_SIDE_VECTOR
    ! LOOP OVER PRODUCT VECTORS
    DO N=1,NTRIALS
     REWIND(37)
     DO I=1,N
      READ(37) XP,YP
     ENDDO
     PXR(N)=DCMPLX(0.0D0,0.0D0)
     DO K=-KVC,MAX(0,KVC-1)
      DO L=1,NOV
       PXR(N)=PXR(N)+DCONJG(XP(L,K))*(-R(L,K))+DCONJG(YP(L,K))*DCONJG(-R(L,K))
      ENDDO
     ENDDO
     REWIND(37)
     ! LOOP OVER PRODUCT VECTORS
     DO M=1,NTRIALS
      READ(37) X,Y
      PXP(M,N)=DCMPLX(0.0D0,0.0D0)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        PXP(M,N)=PXP(M,N)+DCONJG(X(L,K))*XP(L,K)+DCONJG(Y(L,K))*YP(L,K)
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    IF (IOPTN(9) >= 2) THEN
     WRITE(6,'(A)') 'PXP MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(PXP,NTRIALS)
     WRITE(6,'(A)') 'PXR VECTOR IN THE SUBSPACE REPRESENTATION'
     DO I=1,NTRIALS
      WRITE(6,'(2F20.10)') PXR(I)
     ENDDO
    ENDIF

    S=0.0D0
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      IF (CDABS(DCONJG(PXP(M,N))-PXP(N,M)) > S) S=CDABS(DCONJG(PXP(M,N))-PXP(N,M))
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) WRITE(6,'(A,F20.15)') 'DEVIATION FROM HERMITIAN (PXP) = ',S
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      T=(DCONJG(PXP(M,N))+PXP(N,M))/2.0D0
      PXP(M,N)=DCONJG(T)
      PXP(N,M)=T
     ENDDO
    ENDDO

    ! CANONICAL ORTHOGONALIZATION OF PXP MATRIX
    CALL HHBS(NTRIALS,NTRIALS,PXP,WE,WX)
    IF (IOPTN(9) == 3) WRITE(6,'(A,100F10.5:)') 'METRIC  = ',(WE(J),J=1,NTRIALS)
    DO J=1,NTRIALS
     DO K=1,NTRIALS
      WY(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,NTRIALS
       WY(K,J)=WY(K,J)+DCONJG(WX(K,L))*WX(J,L)/WE(L)
      ENDDO
     ENDDO
    ENDDO
    DO J=1,NTRIALS
     C(J)=0.0D0
     DO K=1,NTRIALS
      C(J)=C(J)+WY(J,K)*PXR(K)
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) THEN
     WRITE(6,'(A)') 'SOLUTION OF LINEAR EQUATION'
     DO I=1,NTRIALS
      WRITE(6,'(2F20.10)') C(I)
     ENDDO
    ENDIF

    DEALLOCATE(PXP,PXR,WX,WY,WE)
    ALLOCATE(RX(NOV,-KVC:KVC),RY(NOV,-KVC:KVC))

    ! CALCULATE RESIDUAL VECTOR
    REWIND(36)
    REWIND(37)
    RX=-R
    RY=-R
    DO M=1,NTRIALS
     READ(36) X,Y
     READ(37) XP,YP
     DO K=-KVC,MAX(0,KVC-1)
      DO L=1,NOV
       RX(L,K)=RX(L,K)-C(M)*XP(L,K)
       RY(L,K)=RY(L,K)-C(M)*YP(L,K)
      ENDDO
     ENDDO
    ENDDO
    S=0.0D0
    DO K=-KVC,MAX(0,KVC-1)
     DO L=1,NOV
      S=S+DCONJG(RX(L,K))*RX(L,K)+DCONJG(RY(L,K))*RY(L,K)
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) WRITE(6,'(A,F20.15)') ' NORM OT THE RESIDUAL = ',DSQRT(S)
    IF (IOPTN(9) == 3) THEN
     WRITE(6,'(A,I3)') 'X-COMPONENT OF THE RESIDUAL VECTOR FOR ROOT ',N
     CALL DUMP8(RX,NOV,KVC)
     WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE RESIDUAL VECTOR FOR ROOT ',N
     CALL DUMP8(RY,NOV,KVC)
    ENDIF

    ! GENERATE NEW TRIAL VECTORS ACCORDING TO POPLE
    DO K=-KVC,MAX(0,KVC-1)
     L=0
     DO MOI=ICORE+1,IOCC
      DO MOA=IOCC+1,IALL(K)-IVIRTCORE
       L=L+1
       IF (IMAGINARY) THEN
        NX(L,K)=XP(L,K)/DCMPLX(EPSILON(MOA,K)-EPSILON(MOI,K),-POMEGA)-X(L,K)
        NY(L,K)=YP(L,K)/DCMPLX(EPSILON(MOA,K)-EPSILON(MOI,K),+POMEGA)-Y(L,K)
       ELSE
        NX(L,K)=XP(L,K)/(EPSILON(MOA,K)-EPSILON(MOI,K)-POMEGA)-X(L,K)
        NY(L,K)=YP(L,K)/(EPSILON(MOA,K)-EPSILON(MOI,K)+POMEGA)-Y(L,K)
       ENDIF
      ENDDO
     ENDDO
    ENDDO

    ! COMPUTE POLARIZABILITY
    XP(1:NOV,-KVC:KVC)=DCMPLX(0.0D0)
    YP(1:NOV,-KVC:KVC)=DCMPLX(0.0D0)
    REWIND(36)
    DO M=1,NTRIALS
     READ(36) X,Y
     DO K=-KVC,MAX(0,KVC-1)
      DO L=1,NOV
       XP(L,K)=XP(L,K)+C(M)*X(L,K)
       YP(L,K)=YP(L,K)+C(M)*Y(L,K)
      ENDDO
     ENDDO
    ENDDO
    ALPHA=DCMPLX(0.0D0,0.0D0)
    DO K=-KVC,MAX(0,KVC-1)
     DO L=1,NOV
      ALPHA=ALPHA-2.0D0*(DCONJG(XP(L,K))*R(L,K)+DCONJG(YP(L,K))*DCONJG(R(L,K)))
     ENDDO
    ENDDO
    CALL PCPU_TIME(ICPUE)
!   WRITE(6,'(I3,F13.7,F13.4,F11.1)') ITR,DSQRT(S),ALPHA,ICPUE-ICPUS
    WRITE(6,'(I3,F13.7,2F20.15,F11.1)') ITR,DSQRT(S),ALPHA,ICPUE-ICPUS

    IF (DSQRT(S) < DOPTN(38)) THEN
     WRITE(6,'(A)') '-------------------------------------------------------------------'
     WRITE(6,'(A)') 'CPHF/CPKS CONVERGED'
     WRITE(6,'(A,A1,A1,A,F20.15)') 'POLARIZABILITY ',CXYZ,CXYZ,' = ',DREAL(ALPHA)
     DEALLOCATE(RX,RY,C)
     GOTO 100
    ELSE IF (DSQRT(S) > 2.0D0*PREV) THEN
     CALL WARNING('CPHF/CPKS DID NOT CONVERGE')
     DEALLOCATE(RX,RY,C)
     GOTO 100
    ELSE
     IF (NTRIALS == MAXTRIALS) CALL PABORT('MAXIMUM NUMBER OF TRIAL VECTORS REACHED')
     IF ((IOPTN(88) == 1).OR.(IOPTN(88) == 3).OR.(DSQRT(S) > 0.001D0)) THEN
      WRITE(36) NX,NY
     ELSE IF ((IOPTN(88) == 2).OR.(IOPTN(88) == 4)) THEN
      WRITE(36) RX,RY
     ENDIF 
     NTRIALS=NTRIALS+1
     DEALLOCATE(RX,RY,C)
    ENDIF
    PREV=DSQRT(S)

   ENDDO
   CALL PABORT('CPKS/CPHF DID NOT CONVERGE')

   ! /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\
100 CONTINUE
    POLAR(I1) = DREAL(ALPHA)
   ENDDO
   IF (NMAX > 1) THEN
   WRITE(6,*)
   WRITE(6,'(A)') '------------------------------------------------------'
   WRITE(6,'(A)') 'GRID      OMEGA          WEIGHT            POLAR'
   C6 = 0.0D0
   DO I1 = 1, NMAX
    POMEGA = 1.0D0 / DTAN((2.0D0*DFLOAT(I1)-1.0D0) / (DFLOAT(NMAX) * 4.0D0) * PI)
    WEIGHT = 1.0D0 / DSIN((2.0D0*DFLOAT(I1)-1.0D0) / (DFLOAT(NMAX) * 4.0D0) * PI)**2
    WRITE(6,'(I4,2E15.7,F20.15)') I1,POMEGA,WEIGHT,POLAR(I1)
    C6 = C6 + POLAR(I1)**2 * WEIGHT * 1.5D0 / DFLOAT(NMAX)
   ENDDO
   WRITE(6,'(A)') '------------------------------------------------------'
   WRITE(6,'(A,F20.15)') 'VAN DER WAALS C6 = ',C6
   ENDIF
   ! \/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/

   DEALLOCATE(R,X,Y,XP,YP,NX,NY,PHASE,LDONE)
   DEALLOCATE(POLAR)
   RETURN

END SUBROUTINE



SUBROUTINE MAKE_MULTIPOLE
! COMPUTE AND PRINT DIPOLES AND QUADRUPOLES

   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT

   IMPLICIT NONE

   INTEGER :: I,J,Q
   DOUBLE PRECISION :: DIPOLEX,DIPOLEY,DIPOLEZ
   DOUBLE PRECISION :: QPOLEXX,QPOLEYY,QPOLEZZ
   DOUBLE PRECISION :: QPOLEXY,QPOLEYZ,QPOLEZX
   DOUBLE PRECISION :: NDIPOLEX,NDIPOLEY,NDIPOLEZ
   DOUBLE PRECISION :: NQPOLEXX,NQPOLEYY,NQPOLEZZ
   DOUBLE PRECISION :: NQPOLEXY,NQPOLEYZ,NQPOLEZX

   NDIPOLEX=0.0D0
   NDIPOLEY=0.0D0
   NDIPOLEZ=0.0D0
   NQPOLEXX=0.0D0
   NQPOLEYY=0.0D0
   NQPOLEZZ=0.0D0
   NQPOLEXY=0.0D0
   NQPOLEYZ=0.0D0
   NQPOLEZX=0.0D0
   DO I=1,NATOM
    NDIPOLEX=NDIPOLEX+DFLOAT(IATOM(I))*ATOMX(I)
    NDIPOLEY=NDIPOLEY+DFLOAT(IATOM(I))*ATOMY(I)
    NDIPOLEZ=NDIPOLEZ+DFLOAT(IATOM(I))*ATOMZ(I)
    NQPOLEXX=NQPOLEXX+DFLOAT(IATOM(I))*ATOMX(I)*ATOMX(I)
    NQPOLEYY=NQPOLEYY+DFLOAT(IATOM(I))*ATOMY(I)*ATOMY(I)
    NQPOLEZZ=NQPOLEZZ+DFLOAT(IATOM(I))*ATOMZ(I)*ATOMZ(I)
    NQPOLEXY=NQPOLEXY+DFLOAT(IATOM(I))*ATOMX(I)*ATOMY(I)
    NQPOLEYZ=NQPOLEYZ+DFLOAT(IATOM(I))*ATOMY(I)*ATOMZ(I)
    NQPOLEZX=NQPOLEZX+DFLOAT(IATOM(I))*ATOMZ(I)*ATOMX(I)
   ENDDO
   DIPOLEX=NDIPOLEX
   DIPOLEY=NDIPOLEY
   DIPOLEZ=NDIPOLEZ
   QPOLEXX=NQPOLEXX
   QPOLEYY=NQPOLEYY
   QPOLEZZ=NQPOLEZZ
   QPOLEXY=NQPOLEXY
   QPOLEYZ=NQPOLEYZ
   QPOLEZX=NQPOLEZX
   DO Q=-CEL1,CEL1
    DO I=1,NCGS
     DO J=1,NCGS
      DIPOLEX=DIPOLEX-P_C(J,I,Q)*M_C(J,I,Q,1)
      DIPOLEY=DIPOLEY-P_C(J,I,Q)*M_C(J,I,Q,2)
      DIPOLEZ=DIPOLEZ-P_C(J,I,Q)*M_C(J,I,Q,3)
      QPOLEXX=QPOLEXX-P_C(J,I,Q)*M_C(J,I,Q,4)
      QPOLEYY=QPOLEYY-P_C(J,I,Q)*M_C(J,I,Q,5)
      QPOLEZZ=QPOLEZZ-P_C(J,I,Q)*M_C(J,I,Q,6)
      QPOLEXY=QPOLEXY-P_C(J,I,Q)*M_C(J,I,Q,7)
      QPOLEYZ=QPOLEYZ-P_C(J,I,Q)*M_C(J,I,Q,8)
      QPOLEZX=QPOLEZX-P_C(J,I,Q)*M_C(J,I,Q,9)
     ENDDO
    ENDDO
   ENDDO

   WRITE(6,'(A)') 'DIPOLE MOMENTS IN UNITS OF ATOMIC UNITS'
   WRITE(6,'(A)') '-------------------------------'
   WRITE(6,'(3(A,F8.4))') 'X',DIPOLEX,'  Y',DIPOLEY,'  Z',DIPOLEZ
   WRITE(6,'(A)') '-------------------------------'
   WRITE(6,'(A)') 'QUADRUPOLE MOMENTS IN UNITS OF ATOMIC UNITS' 
   WRITE(6,'(A)') '----------------------------------'
   WRITE(6,'(3(A,F8.4))') 'XX',QPOLEXX,'  YY',QPOLEYY,'  ZZ',QPOLEZZ
   WRITE(6,'(3(A,F8.4))') 'XY',QPOLEXY,'  YZ',QPOLEYZ,'  ZX',QPOLEZX
   WRITE(6,'(A)') '----------------------------------'

   WRITE(6,'(A)') 'DIPOLE MOMENTS IN UNITS OF DEBYE'
   WRITE(6,'(A)') '-------------------------------'
   WRITE(6,'(3(A,F8.4))') 'X',DIPOLEX*DEBYED,'  Y',DIPOLEY*DEBYED,'  Z',DIPOLEZ*DEBYED
   WRITE(6,'(A)') '-------------------------------'
   WRITE(6,'(A)') 'QUADRUPOLE MOMENTS IN UNITS OF DEBYE ANGSTROM'
   WRITE(6,'(A)') '----------------------------------'
   WRITE(6,'(3(A,F8.4))') 'XX',QPOLEXX*DEBYEQ,'  YY',QPOLEYY*DEBYEQ,'  ZZ',QPOLEZZ*DEBYEQ
   WRITE(6,'(3(A,F8.4))') 'XY',QPOLEXY*DEBYEQ,'  YZ',QPOLEYZ*DEBYEQ,'  ZX',QPOLEZX*DEBYEQ
   WRITE(6,'(A)') '----------------------------------'

   RETURN
END SUBROUTINE
