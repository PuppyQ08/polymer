SUBROUTINE THREEINDEX_ERI(TERI,NC,NA,ICEL1,ICEL2)
! CALCULATE THREE-CENTER TWO-ELECTRON ELECTRON REPULSION INTEGRALS
! USING THE OBARA-SAIKA RECURSION METHOD.  THIS SUBROUTINE WORKS CORRECTLY 
! ONLY WHEN HELIX EQUALS TO ZERO.

   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE AUXILIARY
   USE INTEGRAL
   USE GRADIENT
   USE FMT
   USE CONSTANTS
   
   IMPLICIT NONE
   INTEGER :: NC,NA,ICEL1,ICEL2
   INTEGER,PARAMETER :: CASHESIZE = 1000 ! CASHE SIZE MUST BE EXACTLY THE SAME IN THE STORE & RESTORE SUBROUTINES
!  1=S,2=PX,3=PY,4=PZ,5=DXX,6=DYY,7=DZZ,8=DXY,9=DYZ,10=DZX,11=FXXX,12=FYYY,13=FZZZ,14=FXXY,15=FYYZ,16=FZZX,17=FXYY,18=FYZZ,19=FZXX,20=FXYZ,
!  21=GXXXX,22=GYYYY,23=GZZZZ,24=GXXXY,25=GYYYZ,26=GXZZZ,27=GXYYY,28=GYZZZ,29=GXXXZ,30=GXXYY,31=GYYZZ,32=GZZXX,33=GXXYZ,34=GXYYZ,35=GXYZZ
   INTEGER,PARAMETER :: IQ(0:4) = (/ 1, 4,10,20,35/)
   INTEGER,PARAMETER :: IP(35)  &
   = (/ 0, 1, 1, 1, 2, 2, 2, 2, 2, 2, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4/)
   INTEGER,PARAMETER :: NX(35)  &
   = (/ 0, 1, 0, 0, 2, 0, 0, 1, 0, 1, 3, 0, 0, 2, 0, 1, 1, 0, 2, 1, 4, 0, 0, 3, 0, 1, 1, 0, 3, 2, 0, 2, 2, 1, 1/)
   INTEGER,PARAMETER :: NY(35)  &
   = (/ 0, 0, 1, 0, 0, 2, 0, 1, 1, 0, 0, 3, 0, 1, 2, 0, 2, 1, 0, 1, 0, 4, 0, 1, 3, 0, 3, 1, 0, 2, 2, 0, 1, 2, 1/)
   INTEGER,PARAMETER :: NZ(35)  &
   = (/ 0, 0, 0, 1, 0, 0, 2, 0, 1, 1, 0, 0, 3, 0, 1, 2, 0, 2, 1, 1, 0, 0, 4, 0, 1, 3, 0, 3, 1, 0, 2, 2, 1, 1, 2/)
   INTEGER,PARAMETER :: D1X(35) &
   = (/ 0, 1, 0, 0, 2, 0, 0, 3, 0, 4, 5, 0, 0, 8, 0, 7, 6, 0,10, 9,11, 0, 0,14, 0,13,12, 0,19,17, 0,16,20,15,18/)
   INTEGER,PARAMETER :: D1Y(35) &
   = (/ 0, 0, 1, 0, 0, 3, 0, 2, 4, 0, 0, 6, 0, 5, 9, 0, 8, 7, 0,10, 0,12, 0,11,15, 0,17,13, 0,14,18, 0,19,20,16/)
   INTEGER,PARAMETER :: D1Z(35) &
   = (/ 0, 0, 0, 1, 0, 0, 4, 0, 3, 2, 0, 0, 7, 0, 6,10, 0, 9, 5, 8, 0, 0,13, 0,12,16, 0,18,11, 0,15,19,14,17,20/)
   INTEGER,PARAMETER :: D2X(35) &
   = (/ 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 2, 0, 0, 3, 0, 0, 0, 0, 4, 0, 5, 0, 0, 8, 0, 0, 0, 0,10, 6, 0, 7, 9, 0, 0/)
   INTEGER,PARAMETER :: D2Y(35) &
   = (/ 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 3, 0, 0, 4, 0, 2, 0, 0, 0, 0, 6, 0, 0, 9, 0, 8, 0, 0, 5, 7, 0, 0,10, 0/)
   INTEGER,PARAMETER :: D2Z(35) &
   = (/ 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 4, 0, 0, 2, 0, 3, 0, 0, 0, 0, 7, 0, 0,10, 0, 9, 0, 0, 6, 5, 0, 0, 8/)
   INTEGER,PARAMETER :: U1X(35) &
   = (/ 2, 5, 8,10,11,17,16,14,20,19,21,27,26,24,34,32,30,35,29,33, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0/)
   INTEGER,PARAMETER :: U1Y(35) &
   = (/ 3, 8, 6, 9,14,12,18,17,15,20,24,22,28,30,25,35,27,31,33,34, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0/)
   INTEGER,PARAMETER :: U1Z(35) &
   = (/ 4,10, 9, 7,19,15,13,20,18,16,29,25,23,33,31,26,34,28,32,35, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0/)
   INTEGER :: Q1,I,J,K,L,M,II,JJ,LL,I1,J1,L1,TS,MMAX
   INTEGER,ALLOCATABLE :: P2C(:)
   INTEGER,ALLOCATABLE :: SHELL_PGS(:,:),SHELL_CGS(:,:),SHELL_AGS(:,:)
   DOUBLE PRECISION :: PISUB,F1(0:8),F2(0:8)
   DOUBLE PRECISION :: AX,AY,AZ,BX,BY,BZ,CX,CY,CZ,PX,PY,PZ,QX,QY,QZ
   DOUBLE PRECISION :: COMZ,ACOMZ,AGZI,T,H,C
   DOUBLE PRECISION :: TERI(NA,NC,NC,-ICEL1:ICEL1)
   DOUBLE PRECISION,ALLOCATABLE :: ERI(:,:,:,:)
   LOGICAL,ALLOCATABLE :: PRESCREEN(:,:,:)
   
   CALL PABORT('DEBUG THIS SUBROUTINE ! EXAMINE CAREFULLY MMAX')

   ALLOCATE(P2C(NPGS),SHELL_PGS(NPSHELL,20),SHELL_CGS(NPSHELL,20),SHELL_AGS(NASHELL,35),&
   ERI(0:8,20,20,35),PRESCREEN(NPSHELL,NPSHELL,-REDUCED_CEL1:REDUCED_CEL1))

   IF (HELIX /= 0.0D0) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   IF (ICEL1 > REDUCED_CEL1) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   IF ((NCGS >= 256).OR.(NAGS >= 65536).OR.(ICEL1 >= 256).OR.(ICEL2 >= 256)) CALL PABORT('TWO-ELECTRON INTEGRAL FILE IS TOO LARGE')

   ! PRELIMINARY
   DO I=1,NPGS
    K=0
    DO J=1,NCGS
     IF (CC(J,I) /= 0.0D0) THEN
      K=K+1
      P2C(I)=J
     ENDIF
    ENDDO
    IF (K == 0) THEN
     P2C(I)=1
    ELSE IF (K > 1) THEN
     CALL PABORT('GENERAL CONTRACTION IS NOT YET SUPPORTED FOR MP2 CALCULATIONS')
    ENDIF 
   ENDDO
   DO I=1,NPSHELL
    DO J=1,IQ(P_SHELL_ANG(I))
     SHELL_PGS(I,J)=P_SHELL(I,NX(J),NY(J),NZ(J))
     SHELL_CGS(I,J)=P2C(P_SHELL(I,NX(J),NY(J),NZ(J)))
    ENDDO
   ENDDO
   DO I=1,NASHELL
    DO J=1,IQ(A_SHELL_ANG(I))
     SHELL_AGS(I,J)=A_SHELL(I,NX(J),NY(J),NZ(J))
    ENDDO
   ENDDO
   PISUB=2.0D0/DSQRT(PI)
   F1(0)=1.0D0/PISUB
   F1(1)=1.0D0/2.0D0/PISUB
   F1(2)=3.0D0/4.0D0/PISUB
   F1(3)=15.0D0/8.0D0/PISUB
   F1(4)=105.0D0/16.0D0/PISUB
   F1(5)=945.0D0/32.0D0/PISUB
   F1(6)=10395.0D0/64.0D0/PISUB
   F1(7)=135135.0D0/128.0D0/PISUB
   F1(8)=2027025.D0/256.0D0/PISUB
   F2(0)=-0.5D0
   F2(1)=-1.5D0
   F2(2)=-2.5D0
   F2(3)=-3.5D0
   F2(4)=-4.5D0
   F2(5)=-5.5D0
   F2(6)=-6.5D0
   F2(7)=-7.5D0
   F2(8)=-8.5D0
   DO Q1=-REDUCED_CEL1,REDUCED_CEL1
    DO I=1,NPSHELL
     DO J=1,NPSHELL
      PRESCREEN(I,J,Q1)=.TRUE.
      DO I1=1,IQ(P_SHELL_ANG(I))
       DO J1=1,IQ(P_SHELL_ANG(J))
        IF (DABS(S_C(SHELL_CGS(I,I1),SHELL_CGS(J,J1),Q1)) > DOPTN(12)) PRESCREEN(I,J,Q1)=.FALSE.
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO

   TERI=0.0D0
   DO Q1=-ICEL1,ICEL1
    DO I=1,NPSHELL
     II=P_SHELL(I,0,0,0)
     AX=PGSX(II)
     AY=PGSY(II)
     AZ=PGSZ(II)
     DO J=1,NPSHELL
      IF (PRESCREEN(I,J,Q1)) CYCLE
      JJ=P_SHELL(J,0,0,0)
      BX=PGSX(JJ)+DFLOAT(Q1)*PERIOD
      BY=PGSY(JJ)
      BZ=PGSZ(JJ)
      COMZ=ZT(II)+ZT(JJ)
      PX=(AX*ZT(II)+BX*ZT(JJ))/COMZ
      PY=(AY*ZT(II)+BY*ZT(JJ))/COMZ
      PZ=(AZ*ZT(II)+BZ*ZT(JJ))/COMZ
      DO L=1,NASHELL
       LL=A_SHELL(L,0,0,0)
       CX=AGSX(LL)+DFLOAT(ICEL2)*PERIOD
       CY=AGSY(LL)
       CZ=AGSZ(LL)
       ACOMZ=AZT(LL)+COMZ
       AGZI=AZT(LL)*COMZ/ACOMZ
       T=AGZI*((PX-CX)**2+(PY-CY)**2+(PZ-CZ)**2)
       QX=(PX*COMZ+CX*AZT(LL))/ACOMZ
       QY=(PY*COMZ+CY*AZT(LL))/ACOMZ
       QZ=(PZ*COMZ+CZ*AZT(LL))/ACOMZ
       C=S_P(II,JJ,Q1)*2.0D0*PI*DSQRT(AGZI/(AZT(LL)**3))
       DO M=0,P_SHELL_ANG(I)+P_SHELL_ANG(J)+A_SHELL_ANG(L)
        IF (T < TF(M)) THEN
         TS=NINT(T*20.0D0)
         H=0.05D0*DFLOAT(TS)-T
         ERI(M,1,1,1)=((((((IGAMMA(TS,M+6)*H*0.166666666666667D0+IGAMMA(TS,M+5))*H*0.2D0+IGAMMA(TS,M+4))*H*0.25D0+ &
         IGAMMA(TS,M+3))*H*0.333333333333333D0+IGAMMA(TS,M+2))*H*0.5D0+IGAMMA(TS,M+1))*H+IGAMMA(TS,M))*C
        ELSE
         ERI(M,1,1,1)=C*F1(M)*DEXP(F2(M)*DLOG(T))
        ENDIF
       ENDDO
       DO I1=1,IQ(P_SHELL_ANG(I))
        DO J1=1,IQ(P_SHELL_ANG(J))
         DO L1=1,IQ(A_SHELL_ANG(L))
          IF (I1+J1+L1 == 3) CYCLE
          MMAX=P_SHELL_ANG(I)+P_SHELL_ANG(J)+A_SHELL_ANG(L)-IP(I1)+IP(J1)+IP(L1)
          DO M=0,MMAX
           IF (NX(I1) >= 1) THEN
            ERI(M,I1,J1,L1)=(PX-AX)*ERI(M,D1X(I1),J1,L1)+(QX-PX)*ERI(M+1,D1X(I1),J1,L1)
            IF (NX(I1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NX(I1)-1)/COMZ*(ERI(M,D2X(I1),J1,L1)-AGZI/COMZ*ERI(M+1,D2X(I1),J1,L1))
            IF (NX(J1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NX(J1))/COMZ*(ERI(M,D1X(I1),D1X(J1),L1)-AGZI/COMZ*ERI(M+1,D1X(I1),D1X(J1),L1))
            IF (NX(L1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NX(L1))/ACOMZ*ERI(M+1,D1X(I1),J1,D1X(L1))
           ELSE IF (NY(I1) >= 1) THEN
            ERI(M,I1,J1,L1)=(PY-AY)*ERI(M,D1Y(I1),J1,L1)+(QY-PY)*ERI(M+1,D1Y(I1),J1,L1)
            IF (NY(I1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NY(I1)-1)/COMZ*(ERI(M,D2Y(I1),J1,L1)-AGZI/COMZ*ERI(M+1,D2Y(I1),J1,L1))
            IF (NY(J1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NY(J1))/COMZ*(ERI(M,D1Y(I1),D1Y(J1),L1)-AGZI/COMZ*ERI(M+1,D1Y(I1),D1Y(J1),L1))
            IF (NY(L1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NY(L1))/ACOMZ*ERI(M+1,D1Y(I1),J1,D1Y(L1))
           ELSE IF (NZ(I1) >= 1) THEN
            ERI(M,I1,J1,L1)=(PZ-AZ)*ERI(M,D1Z(I1),J1,L1)+(QZ-PZ)*ERI(M+1,D1Z(I1),J1,L1)
            IF (NZ(I1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NZ(I1)-1)/COMZ*(ERI(M,D2Z(I1),J1,L1)-AGZI/COMZ*ERI(M+1,D2Z(I1),J1,L1))
            IF (NZ(J1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NZ(J1))/COMZ*(ERI(M,D1Z(I1),D1Z(J1),L1)-AGZI/COMZ*ERI(M+1,D1Z(I1),D1Z(J1),L1))
            IF (NZ(L1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NZ(L1))/ACOMZ*ERI(M+1,D1Z(I1),J1,D1Z(L1))
           ELSE IF (NX(J1) >= 1) THEN
            ERI(M,I1,J1,L1)=(PX-BX)*ERI(M,I1,D1X(J1),L1)+(QX-PX)*ERI(M+1,I1,D1X(J1),L1)
            IF (NX(J1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NX(J1)-1)/COMZ*(ERI(M,I1,D2X(J1),L1)-AGZI/COMZ*ERI(M+1,I1,D2X(J1),L1))
            IF (NX(L1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NX(L1))/ACOMZ*ERI(M+1,I1,D1X(J1),D1X(L1))
           ELSE IF (NY(J1) >= 1) THEN
            ERI(M,I1,J1,L1)=(PY-BY)*ERI(M,I1,D1Y(J1),L1)+(QY-PY)*ERI(M+1,I1,D1Y(J1),L1)
            IF (NY(J1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NY(J1)-1)/COMZ*(ERI(M,I1,D2Y(J1),L1)-AGZI/COMZ*ERI(M+1,I1,D2Y(J1),L1))
            IF (NY(L1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NY(L1))/ACOMZ*ERI(M+1,I1,D1Y(J1),D1Y(L1))
           ELSE IF (NZ(J1) >= 1) THEN
            ERI(M,I1,J1,L1)=(PZ-BZ)*ERI(M,I1,D1Z(J1),L1)+(QZ-PZ)*ERI(M+1,I1,D1Z(J1),L1)
            IF (NZ(J1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NZ(J1)-1)/COMZ*(ERI(M,I1,D2Z(J1),L1)-AGZI/COMZ*ERI(M+1,I1,D2Z(J1),L1))
            IF (NZ(L1) >= 1) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NZ(L1))/ACOMZ*ERI(M+1,I1,D1Z(J1),D1Z(L1))
           ELSE IF (NX(L1) >= 1) THEN
            ERI(M,I1,J1,L1)=(QX-CX)*ERI(M+1,I1,J1,D1X(L1))
            IF (NX(L1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NX(L1)-1)/AZT(LL)*(ERI(M,I1,J1,D2X(L1))-AGZI/AZT(LL)*ERI(M+1,I1,J1,D2X(L1)))
           ELSE IF (NY(L1) >= 1) THEN
            ERI(M,I1,J1,L1)=(QY-CY)*ERI(M+1,I1,J1,D1Y(L1))
            IF (NY(L1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NY(L1)-1)/AZT(LL)*(ERI(M,I1,J1,D2Y(L1))-AGZI/AZT(LL)*ERI(M+1,I1,J1,D2Y(L1)))
           ELSE IF (NZ(L1) >= 1) THEN
            ERI(M,I1,J1,L1)=(QZ-CZ)*ERI(M+1,I1,J1,D1Z(L1))
            IF (NZ(L1) >= 2) ERI(M,I1,J1,L1)=&
   ERI(M,I1,J1,L1)+0.5D0*DFLOAT(NZ(L1)-1)/AZT(LL)*(ERI(M,I1,J1,D2Z(L1))-AGZI/AZT(LL)*ERI(M+1,I1,J1,D2Z(L1)))
           END IF
          ENDDO
         ENDDO
        ENDDO
       ENDDO
       DO I1=1,IQ(P_SHELL_ANG(I))
        DO J1=1,IQ(P_SHELL_ANG(J))
         DO L1=1,IQ(A_SHELL_ANG(L))
          TERI(SHELL_AGS(L,L1),SHELL_CGS(I,I1),SHELL_CGS(J,J1),Q1)=&
   TERI(SHELL_AGS(L,L1),SHELL_CGS(I,I1),SHELL_CGS(J,J1),Q1)+&
   ERI(0,I1,J1,L1)*CC(SHELL_CGS(I,I1),SHELL_PGS(I,I1))*CC(SHELL_CGS(J,J1),SHELL_PGS(J,J1))*ANORM(SHELL_AGS(L,L1))
         ENDDO
        ENDDO
       ENDDO
      ENDDO
     ENDDO
    ENDDO
   ENDDO
   
   DEALLOCATE(P2C,SHELL_PGS,SHELL_CGS,SHELL_AGS,ERI,PRESCREEN)
   RETURN
END SUBROUTINE
