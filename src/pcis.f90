SUBROUTINE GUESS_TRIALVECTOR(LTDR,LAPPROX)
! CONSTRUCT INITIAL TRIAL VECTORS FOR CIS/TDR CALCULATIONS AND STORE THEM ON DISK.

   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE CIS

   IMPLICIT NONE
   INTEGER :: I,J,K,L
   DOUBLE PRECISION :: A
   LOGICAL :: LTDR,LDONE,LAPPROX
   INTEGER,ALLOCATABLE :: SORT(:)
   DOUBLE PRECISION,ALLOCATABLE :: H(:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: X(:,:),Y(:,:)

   NOV=(IOCC-ICORE)*(IALLMAX-IOCC-IVIRTCORE)
   IF (LTDR) THEN
    IF (DOPTN(24) == 0.0D0) THEN
     WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY TIME DEPENDENT RESPONSE THEORY'
    ELSE IF (LOPTN(32)) THEN
     WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY DYSON QUASI-PARTICLE TIME DEPENDENT RESPONSE THEORY'
    ELSE
     WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY MP2 QUASI-PARTICLE TIME DEPENDENT RESPONSE THEORY'
    ENDIF
    NROOTS=IOPTN(37)
   ELSE
    IF (DOPTN(24) == 0.0D0) THEN
     WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY CONFIGURATION INTERACTION SINGLES THEORY'
    ELSE
     IF (LAPPROX) THEN
      IF (LOPTN(32)) THEN
       WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY CONFIGURATION INTERACTION SINGLES DYSON QUASI-PARTICLE THEORY'
      ELSE
       WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY CONFIGURATION INTERACTION SINGLES MP2 QUASI-PARTICLE THEORY'
      ENDIF
     ELSE
      IF (LOPTN(32)) THEN
       WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY DYSON QUASI-PARTICLE CONFIGURATION INTERACTION SINGLES THEORY'
      ELSE
       WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY MP2 QUASI-PARTICLE CONFIGURATION INTERACTION SINGLES THEORY'
      ENDIF
     ENDIF
    ENDIF
    NROOTS=IOPTN(36)
   ENDIF
   IF (NROOTS > NOV*MAX(1,2*KVC)) THEN
    CALL WARNING('NUMBER OF ROOTS HAS BEEN REDUCED')
    NROOTS=NOV*MAX(1,2*KVC)
   ENDIF
   WRITE(6,'(A,I3)') 'NUMBER OF ROOTS REQUESTED       = ',NROOTS
   MAXTRIALS=MIN(INT(DOPTN(26)*1000000.0D0/(DFLOAT(NOV*MAX(1,2*KVC))*32.0D0)),NOV*MAX(1,2*KVC))
   IF (MAXTRIALS < NROOTS) CALL PABORT('INSUFFICIENT DISKSPACE')
   WRITE(6,'(A,I3)') 'MAXIMUM NUMBER OF TRIAL VECTORS = ',MAXTRIALS

   ALLOCATE(SORT(NOV*MAX(1,2*KVC)),H(NOV,-KVC:KVC),X(NOV,-KVC:KVC),Y(NOV,-KVC:KVC))

   ! CALCULATE ONE-ELECTRON ENERGY DIFFERENCES
   DO K=-KVC,MAX(0,KVC-1)
    L=0
    DO I=ICORE+1,IOCC
     DO J=IOCC+1,IALL(K)-IVIRTCORE
      L=L+1
      IF ((DOPTN(24) == 0.0D0).OR.(LAPPROX)) THEN
       H(L,K)=EPSILON(J,K)-EPSILON(I,K)
      ELSE IF (LOPTN(32)) THEN
       H(L,K)=DEPSILON(J,K)-DEPSILON(I,K)
      ELSE
       H(L,K)=QEPSILON(J,K)-QEPSILON(I,K)
      ENDIF
      IF (IOPTN(9) >= 2) WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F10.4)') ' V(',J,'[',K,']) - O(',I,'[',K,']) = ',H(L,K)
     ENDDO
    ENDDO
    IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   ENDDO

   ! SORT THE ONE-ELECTRON ENERGY DIFFERENCES
   DO I=1,NOV*MAX(1,2*KVC)
    A=1.0D99
    DO K=-KVC,MAX(0,KVC-1)
     DO L=1,NOV
      IF (H(L,K) < A) THEN
       IF (I == 1) THEN
        A=H(L,K)
        SORT(I)=(L-1)+(K+KVC)*NOV
       ELSE
        LDONE=.FALSE.
        DO J=1,I-1
         IF (SORT(J) == (L-1)+(K+KVC)*NOV) LDONE=.TRUE.
        ENDDO
        IF (.NOT.LDONE) THEN
         A=H(L,K)
         SORT(I)=(L-1)+(K+KVC)*NOV
        ENDIF
       ENDIF
      ENDIF
     ENDDO
    ENDDO
   ENDDO
   IF (IOPTN(9) == 3) THEN
    WRITE(6,'(A)') 'EXCITATION ENERGIES IN ASCENDING ORDER'
    DO I=1,NOV*MAX(1,2*KVC)
     WRITE(6,'(I3,F10.4)') I,H(MOD(SORT(I),NOV)+1,SORT(I)/NOV-KVC)
    ENDDO
   ENDIF

   ! INCREASE THE NUMBER OF ROOTS IF NECESSARY
   J=NROOTS
   IF (NROOTS < NOV*MAX(1,2*KVC)) THEN
    DO I=NROOTS+1,NOV*MAX(1,2*KVC)
     IF (H(MOD(SORT(I),NOV)+1,SORT(I)/NOV-KVC)-H(MOD(SORT(NROOTS),NOV)+1,SORT(NROOTS)/NOV-KVC) < THRESH) J=J+1
    ENDDO
   ENDIF
   IF (J > NROOTS) THEN
    NROOTS=J
    WRITE(6,'(A,I3)') 'NUMBER OF ROOTS HAS BEEN INCREASED TO ',NROOTS
   ENDIF

   ! CONSTRUCT INITIAL TRIAL VECTORS AND STORE
   REWIND(36)
   DO I=1,NROOTS
    X(1:NOV,-KVC:KVC)=DCMPLX(0.0D0,0.0D0)
    Y(1:NOV,-KVC:KVC)=DCMPLX(0.0D0,0.0D0)
    X(MOD(SORT(I),NOV)+1,SORT(I)/NOV-KVC)=DCMPLX(1.0D0,0.0D0)
    WRITE(36) X,Y
   ENDDO

   NTRIALS=NROOTS

   DEALLOCATE(SORT,H,X,Y)
END SUBROUTINE



SUBROUTINE OLSEN_DIAGONALIZATION(LTDR,IFLAG,LAPPROX)
! DIRECT DAVIDSON-LIKE ALGORITHMS FOR SOLVING LARGE NON-HERMITIAN CIS/TDR EIGENVALUE PROBLEMS.
! SEE E.R.DAVIDSON,J.COMPUT.PHYS.17,87(1975);J.OLSEN,H.JORGEN,AA.JENSEN,AND P.JORGENSEN,J.COMPUT.PHYS.74,265(1988).
! SEE R.BAUERNSCHMITT AND R.AHLRICHS,CHEM.PHYS.LETT.256,454(1996);C.JAMORSKI,M.CASIDA,AND D.R.SALAHUB,J.CHEM.PHYS.104,5134(1996).
! SEE M.G.VRACKO AND M.ZAIDER,INT.J.QUANTUM.CHEM.43,321(1992);47,119(1993);
! M.VRACKO,B.CHAMPAGNE,D.H.MOSLEY,AND J.-M.ANDRE,J.CHEM.PHYS.102,6831(1995).
! IFLAG = 1 : SINGLET EXCITATION ENERGY CALCULATIONS.
! IFLAG = 2 : TRIPLET EXCITATION ENERGY CALCULATIONS.
! COMMENTS : THERE IS A VERY SUBTLE PROBLEM AS TO THE CUTOFF OF EXCHANGE LATTICE SUMMATIONS,
!            SINCE UNLIKE GROUND STATE CALCULATIONS THE DENSITY MATRIX DOES NOT NECESARRILY DECAY.
!            NUMERICAL EXPERIENCE SHOWS THAT THE LOWEST ROOT MAY BE COMPUTED WITH TORELABLE ACCURACY.

   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT
   USE CIS

   IMPLICIT NONE
   REAL :: ICPUS,ICPUE
   INTEGER :: IFLAG
   INTEGER :: I,J,K,L,M,N,O,Q,MOI,MOA,ITR,NDISCARDS
   LOGICAL :: LTDR,LAPPROX
   DOUBLE PRECISION :: S,MAXVEC,MAXENG
   DOUBLE PRECISION :: CISD,CISQ
   DOUBLE COMPLEX :: T
   DOUBLE COMPLEX,ALLOCATABLE :: X(:,:),Y(:,:)               ! TRIAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: XP(:,:),YP(:,:)             ! PRODUCT VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: RX(:,:),RY(:,:)             ! RESIDUAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: P_AX(:,:,:),P_AY(:,:,:)     ! TRIAL DENSITY MATRIX FOR ONE TRIAL VECTOR
   DOUBLE COMPLEX,ALLOCATABLE :: P_BX(:,:,:),P_BY(:,:,:)     ! TRIAL DENSITY MATRIX FOR ONE TRIAL VECTOR
   DOUBLE COMPLEX,ALLOCATABLE :: A_X(:,:,:),A_Y(:,:,:),B_X(:,:,:),B_Y(:,:,:) ! PRODUCT DENSITY MATRICES
   DOUBLE COMPLEX,ALLOCATABLE :: A(:,:),B(:,:),C(:,:),D(:,:) ! SUBSPACE REPRESENTATION FOR A,B, AND METRIC MATRICES
   DOUBLE COMPLEX,ALLOCATABLE :: WA(:,:),WC(:,:),WX(:,:)     ! SUBSPACE REPRESENTATION WITH THE DOUBLE DIMENSION
   DOUBLE COMPLEX,ALLOCATABLE :: UNITC(:,:),UNITX(:,:)       ! SUBSPACE REPRESENTATION FOR EUCLIDEAN NORM
   DOUBLE COMPLEX,ALLOCATABLE :: PHASE(:)
   DOUBLE PRECISION,ALLOCATABLE :: WE(:),OMEGA(:),DEV(:)     ! EIGENVALUES OF SUBSPACE EIGENEQUATION,SQUARED NORM OF RESIDUAL
   DOUBLE PRECISION,ALLOCATABLE :: UNITE(:)                  ! METRIC OF EUCLIDEAN NORM
   DOUBLE PRECISION,ALLOCATABLE :: PREV_OMEGA(:)             ! EIGENVALUES OF THE PREVIOUS ITERATION
   LOGICAL,ALLOCATABLE :: LDONE(:)                           ! .TRUE. WHEN PRODUCT FORMATION HAS ALREADY BEEN DONE

   ALLOCATE(PHASE(-CEL1*KVC:CEL1*KVC))
   ALLOCATE(DEV(NROOTS),LDONE(MAXTRIALS),PREV_OMEGA(NROOTS))
   ALLOCATE(X(NOV,-KVC:KVC),Y(NOV,-KVC:KVC))
   ALLOCATE(XP(NOV,-KVC:KVC),YP(NOV,-KVC:KVC))

   IF (IFLAG == 1) THEN
    WRITE(6,'(A)') 'CALCULATE SINGLET EXCITATION ENERGIES BY OLSEN ALGORITHM'
   ELSE IF (IFLAG == 2) THEN
    WRITE(6,'(A)') 'CALCULATE TRIPLET EXCITATION ENERGIES BY OLSEN ALGORITHM'
   ENDIF
   WRITE(6,'(A)') '----------------------------------------------------------------------------'
   WRITE(6,'(A)') 'ITR  TRIAL VECTORS  RTS LEFT  RTS CNVRGD   ENERGYDEV   VECTORDEV   CPU / SEC'

   ! PRECALCULATE PHASE FACTORS
   DO I=-CEL1*KVC,CEL1*KVC
    IF (KVC == 0) THEN
     PHASE(I)=DCMPLX(1.0D0,0.0D0)
    ELSE
     PHASE(I)=DCMPLX(DCOS(DFLOAT(I)/DFLOAT(KVC)*PI),DSIN(DFLOAT(I)/DFLOAT(KVC)*PI))
    ENDIF
   ENDDO
   LDONE(1:MAXTRIALS)=.FALSE.
   PREV_OMEGA(1:NROOTS)=9.9D99
   ITR=0

   ! MAIN LOOP
   DO
    ITR=ITR+1
    REWIND(36)
    REWIND(37)

    ! LOOP OVER TRIAL VECTORS

    ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1),P_BX(NCGS,NCGS,-CEL1:CEL1),P_AY(NCGS,NCGS,-CEL1:CEL1),P_BY(NCGS,NCGS,-CEL1:CEL1))
    ALLOCATE(A_X(NCGS,NCGS,-CEL1:CEL1),A_Y(NCGS,NCGS,-CEL1:CEL1),B_X(NCGS,NCGS,-CEL1:CEL1),B_Y(NCGS,NCGS,-CEL1:CEL1))

    CALL PCPU_TIME(ICPUS)
    DO N=1,NTRIALS

     ! IF THE PRODUCT FORMATION HAS ALREADY BEEN DONE, JUST PROCEED
     IF (LDONE(N)) THEN
      READ(36) X,Y
      READ(37) X,Y
     ELSE
      LDONE(N)=.TRUE.
      ! TRANSFORM TRIAL VECTORS TO TRIAL DENSITY MATRICES
      READ(36) X,Y
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'X-COMPONENT OF THE TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(X,NOV,KVC)
       WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(Y,NOV,KVC)
      ENDIF
      DO I=1,NCGS
       DO J=1,NCGS
        DO Q=-CEL1,CEL1
         P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         P_BY(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         P_BX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         P_AY(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         DO K=-KVC,MAX(0,KVC-1)
          L=0
          DO MOI=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(K)-IVIRTCORE
            L=L+1
            P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*X(L,K)*PHASE(K*Q)
            P_BY(I,J,Q)=P_BY(I,J,Q)+DCONJG(CO(I,MOA,K))*CO(J,MOI,K)*Y(L,K)*PHASE(K*Q)
            P_BX(I,J,Q)=P_BX(I,J,Q)+CO(I,MOA,K)*DCONJG(CO(J,MOI,K))*X(L,K)*PHASE(-K*Q)
            P_AY(I,J,Q)=P_AY(I,J,Q)+CO(I,MOI,K)*DCONJG(CO(J,MOA,K))*Y(L,K)*PHASE(-K*Q)
           ENDDO
          ENDDO
          IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
         ENDDO
         P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
         P_BY(I,J,Q)=P_BY(I,J,Q)/DFLOAT(MAX(1,2*KVC))
         P_BX(I,J,Q)=P_BX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
         P_AY(I,J,Q)=P_AY(I,J,Q)/DFLOAT(MAX(1,2*KVC))
        ENDDO
       ENDDO
      ENDDO
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'AX-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_AX,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'BY-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_BY,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'BX-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_BX,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'AY-COMPONENT OF THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_AY,NCGS,CEL1)
      ENDIF

      ! ZERO SCRATCH PRODUCT MATRICES
      A_X(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      B_Y(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      B_X(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      A_Y(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)

      ! CONTRACT TRIAL DENSITY MATRICES WITH OEP KERNEL
      IF (DOPTN(84) /= 0.0D0) THEN
       CALL CONTRACT_OEP(A_X,P_AX,NCGS,CEL1,IFLAG)
       IF (LTDR) THEN
        CALL CONTRACT_OEP(B_Y,P_BY,NCGS,CEL1,IFLAG)
        CALL CONTRACT_OEP(B_X,P_BX,NCGS,CEL1,IFLAG)
        CALL CONTRACT_OEP(A_Y,P_AY,NCGS,CEL1,IFLAG)
       ENDIF
      ENDIF

      ! CONTRACT TRIAL DENSITY MATRICES WITH TWO-ELECTRON INTEGRALS
      IF (LOPTN(25)) THEN
       CALL DIRECT_CONTRACT_FOURINDEX_ERI(LTDR,A_X,P_AX,B_Y,P_BY,B_X,P_BX,A_Y,P_AY,NCGS,CEL1,IFLAG)
      ELSE
       CALL CONTRACT_FOURINDEX_ERI(A_X,P_AX,NCGS,CEL1,IFLAG)
       IF (LTDR) THEN
        CALL CONTRACT_FOURINDEX_ERI(B_Y,P_BY,NCGS,CEL1,IFLAG)
        CALL CONTRACT_FOURINDEX_ERI(B_X,P_BX,NCGS,CEL1,IFLAG)
        CALL CONTRACT_FOURINDEX_ERI(A_Y,P_AY,NCGS,CEL1,IFLAG)
       ENDIF
      ENDIF

      ! CONTRACT TRIAL DENSITY MATRICES WITH EXCHANGE-CORRELATION INTEGRALS
      IF (LDFT) THEN
       D2F1=0.0D0
       IF (LGC) THEN
        D1F2=0.0D0
        D2F2=0.0D0
        D2F3=0.0D0
       ENDIF
      ENDIF
      IF (DOPTN(20) /= 0.0D0) CALL SLATER_EXCHANGE(IFLAG)
      IF (DOPTN(21) /= 0.0D0) CALL VOSKO_WILK_NUSAIR_CORRELATION(IFLAG)
      IF (DOPTN(22) /= 0.0D0) CALL BECKE88_EXCHANGE(IFLAG)
      IF (DOPTN(23) /= 0.0D0) CALL LEE_YANG_PARR_CORRELATION(IFLAG)
       IF (LDFT.AND.(.NOT.(LGC))) THEN
       CALL TRIAL_ELECTRON_DENSITY(P_AX,NCGS,CEL1)
       CALL CONTRACT_LOCAL_XC(A_X,NCGS,CEL1)
       IF (LTDR) THEN
        CALL TRIAL_ELECTRON_DENSITY(P_BY,NCGS,CEL1)
        CALL CONTRACT_LOCAL_XC(B_Y,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY(P_BX,NCGS,CEL1)
        CALL CONTRACT_LOCAL_XC(B_X,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY(P_AY,NCGS,CEL1)
        CALL CONTRACT_LOCAL_XC(A_Y,NCGS,CEL1)
       ENDIF
      ELSE IF (LDFT.AND.LGC) THEN
       CALL TRIAL_ELECTRON_DENSITY(P_AX,NCGS,CEL1)
       CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_AX,NCGS,CEL1)
       CALL CONTRACT_GC_XC(A_X,NCGS,CEL1)
       IF (LTDR) THEN
        CALL TRIAL_ELECTRON_DENSITY(P_BY,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_BY,NCGS,CEL1)
        CALL CONTRACT_GC_XC(B_Y,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY(P_BX,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_BX,NCGS,CEL1)
        CALL CONTRACT_GC_XC(B_X,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY(P_AY,NCGS,CEL1)
        CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_AY,NCGS,CEL1)
        CALL CONTRACT_GC_XC(A_Y,NCGS,CEL1)
       ENDIF
      ENDIF
       
      ! SET B_X, B_Y AND A_Y ZERO FOR CIS CALCULATIONS
      IF (.NOT.LTDR) THEN
       B_X(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
       B_Y(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
       A_Y(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)
      ENDIF
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'A TIMES X FOR ROOT ',N
       CALL DUMP9(A_X,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'B TIMES Y FOR ROOT ',N
       CALL DUMP9(B_Y,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'B TIMES X FOR ROOT ',N
       CALL DUMP9(B_X,NCGS,CEL1)
       WRITE(6,'(A,I3)') 'A TIMES Y FOR ROOT ',N
       CALL DUMP9(A_Y,NCGS,CEL1)
      ENDIF

      ! BACK-TRANSFORM PRODUCT DENSITY MATRICES TO PRODUCT VECTORS
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         XP(L,K)=DCMPLX(0.0D0,0.0D0)
         YP(L,K)=DCMPLX(0.0D0,0.0D0)
         DO I=1,NCGS
          DO J=1,NCGS
           DO Q=-CEL1,CEL1
            XP(L,K)=XP(L,K)+DCONJG(CO(I,MOA,K))*CO(J,MOI,K)*(A_X(I,J,Q)+B_Y(I,J,Q))*PHASE(K*Q)
            YP(L,K)=YP(L,K)+CO(I,MOA,K)*DCONJG(CO(J,MOI,K))*(B_X(I,J,Q)+A_Y(I,J,Q))*PHASE(-K*Q)
           ENDDO
          ENDDO
         ENDDO
         ! ADD ONE-ELECTRON ENERGY DIFFERENCES
         IF ((DOPTN(24) == 0.0D0).OR.(LAPPROX)) THEN
          XP(L,K)=XP(L,K)+(EPSILON(MOA,K)-EPSILON(MOI,K))*X(L,K)
          YP(L,K)=YP(L,K)+(EPSILON(MOA,K)-EPSILON(MOI,K))*Y(L,K)
         ELSE IF (LOPTN(32)) THEN
          XP(L,K)=XP(L,K)+(DEPSILON(MOA,K)-DEPSILON(MOI,K))*X(L,K)
          YP(L,K)=YP(L,K)+(DEPSILON(MOA,K)-DEPSILON(MOI,K))*Y(L,K)
         ELSE
          XP(L,K)=XP(L,K)+(QEPSILON(MOA,K)-QEPSILON(MOI,K))*X(L,K)
          YP(L,K)=YP(L,K)+(QEPSILON(MOA,K)-QEPSILON(MOI,K))*Y(L,K)
         ENDIF
        ENDDO
       ENDDO
      ENDDO

      ! STORE THE PRODUCT VECTORS
      WRITE(37) XP,YP
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'X-COMPONENT OF THE PRODUCT VECTOR FOR ROOT ',N
       CALL DUMP8(XP,NOV,KVC)
       WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE PRODUCT VECTOR FOR ROOT ',N
       CALL DUMP8(YP,NOV,KVC)
      ENDIF

     ENDIF
    ENDDO

    DEALLOCATE(P_AX,P_BX,P_AY,P_BY,A_X,B_X,A_Y,B_Y)
    ALLOCATE(A(NTRIALS,NTRIALS),B(NTRIALS,NTRIALS),C(NTRIALS,NTRIALS),D(NTRIALS,NTRIALS))
    ALLOCATE(WA(2*NTRIALS,2*NTRIALS),WC(2*NTRIALS,2*NTRIALS),WX(2*NTRIALS,2*NTRIALS),WE(2*NTRIALS))
    ALLOCATE(OMEGA(2*NTRIALS))

    ! FORM A AND B MATRICES IN THE SUBSPACE SPANNED BY THE TRIAL VECTORS
    REWIND(37)
    ! LOOP OVER PRODUCT VECTORS
    DO N=1,NTRIALS
     READ(37) XP,YP
     REWIND(36)
     ! LOOP OVER TRIAL VECTORS
     DO M=1,NTRIALS
      READ(36) X,Y
      A(M,N)=DCMPLX(0.0D0,0.0D0)
      B(M,N)=DCMPLX(0.0D0,0.0D0)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        A(M,N)=A(M,N)+DCONJG(X(L,K))*XP(L,K)+DCONJG(Y(L,K))*YP(L,K)
        IF (LTDR) B(M,N)=B(M,N)+DCONJG(X(L,K))*DCONJG(YP(L,K))+DCONJG(Y(L,K))*DCONJG(XP(L,K))
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    ! FORM C AND D MATRICES IN THE SUBSPACE SPANNED BY THE TRIAL VECTORS
    ! LOOP OVER TRIAL VECTORS
    DO N=1,NTRIALS
     REWIND(36)
     DO I=1,N
      READ(36) XP,YP
     ENDDO
     REWIND(36)
     ! LOOP OVER TRIAL VECTORS
     DO M=1,NTRIALS
      READ(36) X,Y
      C(M,N)=DCMPLX(0.0D0,0.0D0)
      D(M,N)=DCMPLX(0.0D0,0.0D0)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        C(M,N)=C(M,N)+DCONJG(X(L,K))*XP(L,K)-DCONJG(Y(L,K))*YP(L,K) ! HERE XP IS ACTUALLY X, AND YP IS Y
        IF (LTDR) D(M,N)=D(M,N)+DCONJG(X(L,K))*DCONJG(YP(L,K))-DCONJG(Y(L,K))*DCONJG(XP(L,K))
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    IF (IOPTN(9) >= 2) THEN
     WRITE(6,'(A)') 'A MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(A,NTRIALS)
     WRITE(6,'(A)') 'B MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(B,NTRIALS)
     WRITE(6,'(A)') 'C MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(C,NTRIALS)
     WRITE(6,'(A)') 'D MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(D,NTRIALS)
    ENDIF

    ! FORM (A,B) AND (C,D) MATRICES FROM A,B,C,AND D
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      WA(M,        N        )=A(M,N)
      WA(M,        N+NTRIALS)=B(M,N)
      WA(M+NTRIALS,N        )=DCONJG(B(M,N))
      WA(M+NTRIALS,N+NTRIALS)=DCONJG(A(M,N))
      WC(M,        N        )=C(M,N)
      WC(M,        N+NTRIALS)=D(M,N)
      WC(M+NTRIALS,N        )=-DCONJG(D(M,N))
      WC(M+NTRIALS,N+NTRIALS)=-DCONJG(C(M,N))
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) THEN
     WRITE(6,'(A)') 'A,B MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(WA,2*NTRIALS)
     WRITE(6,'(A)') 'C,D MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(WC,2*NTRIALS)
    ENDIF
    S=0.0D0
    DO N=1,2*NTRIALS
     DO M=1,2*NTRIALS
      IF (CDABS(DCONJG(WA(M,N))-WA(N,M)) > S) S=CDABS(DCONJG(WA(M,N))-WA(N,M))
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) WRITE(6,'(A,F20.15)') 'DEVIATION FROM HERMITIAN (A,B) = ',S
    DO N=1,2*NTRIALS
     DO M=1,2*NTRIALS
      T=(DCONJG(WA(M,N))+WA(N,M))/2.0D0
      WA(M,N)=DCONJG(T)
      WA(N,M)=T
     ENDDO
    ENDDO
    S=0.0D0
    DO N=1,2*NTRIALS
     DO M=1,2*NTRIALS
      IF (CDABS(DCONJG(WC(M,N))-WC(N,M)) > S) S=CDABS(DCONJG(WC(M,N))-WC(N,M))
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) WRITE(6,'(A,F20.15)') 'DEVIATION FROM HERMITIAN (C,D) = ',S
    DO N=1,2*NTRIALS
     DO M=1,2*NTRIALS
      T=(DCONJG(WC(M,N))+WC(N,M))/2.0D0
      WC(M,N)=DCONJG(T)
      WC(N,M)=T
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) THEN
     WRITE(6,'(A)') 'A,B MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(WA,2*NTRIALS)
     WRITE(6,'(A)') 'C,D MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(WC,2*NTRIALS)
    ENDIF

    ! CANONICAL ORTHOGONALIZATION OF (A,B) MATRIX
    CALL HHBS(2*NTRIALS,2*NTRIALS,WA,WE,WX)
    IF (IOPTN(9) == 3) WRITE(6,'(A,100F10.5:)') 'METRIC  = ',(WE(J),J=1,2*NTRIALS)
    DO J=1,2*NTRIALS
     IF (WE(J) <= 0.0D0) CALL PABORT('METRIC IS NOT POSITIVE DEFINITE')
     DO K=1,2*NTRIALS
      WA(K,J)=WX(K,J)/DSQRT(WE(J))
     ENDDO
    ENDDO

    ! DIAGONALIZATION OF (C,D) MATRIX
    DO J=1,2*NTRIALS
     DO K=1,2*NTRIALS
      WX(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,2*NTRIALS
       WX(K,J)=WX(K,J)+WC(K,L)*WA(L,J)
      ENDDO
     ENDDO
    ENDDO
    DO J=1,2*NTRIALS
     DO K=1,2*NTRIALS
      WC(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,2*NTRIALS
       WC(K,J)=WC(K,J)+DCONJG(WA(L,K))*WX(L,J)
      ENDDO
     ENDDO
    ENDDO
    CALL HHBS(2*NTRIALS,2*NTRIALS,WC,WE,WX)
    IF (IOPTN(9) >= 2) WRITE(6,'(A,100F10.5:)') 'OMEGA = ',(1.0D0/WE(J),J=2*NTRIALS,NTRIALS+1,-1)
    IF (WE(NTRIALS+1) <= 0.0D0) CALL PABORT('NEGATIVE OR INFINITE EXCITATION ENERGY')
    DO J=1,2*NTRIALS
     OMEGA(J)=1.0D0/WE(J)
     DO K=1,2*NTRIALS
      WC(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,2*NTRIALS
       WC(K,J)=WC(K,J)+WA(K,L)*WX(L,J)
      ENDDO
     ENDDO
     IF (IOPTN(9) == 3) THEN
      WRITE(6,'(A,I3,A,F10.6,A,F10.4,A)') 'ROOT No.',J,' EXCITATION ENERGY / HARTREE = ',OMEGA(J),' ( ',OMEGA(J)*EV,' )'
      DO K=1,2*NTRIALS
       WRITE(6,'(A,I3,A,2F8.4)') '  TRIAL VECTOR No.',K,'  TIMES ',WC(K,J)
      ENDDO
     ENDIF
    ENDDO

    ALLOCATE(RX(NOV,-KVC:KVC),RY(NOV,-KVC:KVC))

    ! CALCULATE RESIDUAL VECTOR FOR EACH ROOT
    MAXVEC=0.0D0
    MAXENG=0.0D0
    NCONVS=0
    REWIND(38)
    DO N=1,NROOTS
     REWIND(36)
     REWIND(37)
     RX(1:NOV,-KVC:KVC)=DCMPLX(0.0D0,0.0D0)
     RY(1:NOV,-KVC:KVC)=DCMPLX(0.0D0,0.0D0)
     DO M=1,NTRIALS
      READ(36) X,Y
      READ(37) XP,YP
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        RX(L,K)=RX(L,K)+WC(M,2*NTRIALS-N+1)*XP(L,K)-WC(M,2*NTRIALS-N+1)*X(L,K)*OMEGA(2*NTRIALS-N+1) &
                       +WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(YP(L,K))-WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(Y(L,K))*OMEGA(2*NTRIALS-N+1)
        RY(L,K)=RY(L,K)+WC(M,2*NTRIALS-N+1)*YP(L,K)+WC(M,2*NTRIALS-N+1)*Y(L,K)*OMEGA(2*NTRIALS-N+1) &
                       +WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(XP(L,K))+WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(X(L,K))*OMEGA(2*NTRIALS-N+1)
       ENDDO
      ENDDO
     ENDDO
     DEV(N)=0.0D0
     DO K=-KVC,MAX(0,KVC-1)
      DO L=1,NOV
       DEV(N)=DEV(N)+DCONJG(RX(L,K))*RX(L,K)+DCONJG(RY(L,K))*RY(L,K)
      ENDDO
     ENDDO
     IF (IOPTN(9) == 3) WRITE(6,'(A,I3,A,F20.15)') 'ROOT No.',N,' NORM OT THE RESIDUAL = ',DSQRT(DEV(N))
     IF (DSQRT(DEV(N)) > MAXVEC) MAXVEC=DSQRT(DEV(N))
     IF (DABS(OMEGA(2*NTRIALS-N+1)-PREV_OMEGA(N)) > MAXENG) MAXENG=DABS(OMEGA(2*NTRIALS-N+1)-PREV_OMEGA(N))
     IF (IOPTN(9) == 3) THEN
      WRITE(6,'(A,I3)') 'X-COMPONENT OF THE RESIDUAL VECTOR FOR ROOT ',N
      CALL DUMP8(RX,NOV,KVC)
      WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE RESIDUAL VECTOR FOR ROOT ',N
      CALL DUMP8(RY,NOV,KVC)
     ENDIF
!    IF ((DSQRT(DEV(N)) < DOPTN(38)).AND.(DABS(OMEGA(2*NTRIALS-N+1)-PREV_OMEGA(N)) < DOPTN(38)/10.0D0)) THEN
!    IF ((DABS(OMEGA(2*NTRIALS-N+1)-PREV_OMEGA(N)) < DOPTN(38)).OR.((DSQRT(DEV(N)) < DOPTN(38)).AND.(KVC == 0))) THEN
     IF (((DABS(OMEGA(2*NTRIALS-N+1)-PREV_OMEGA(N)) < DOPTN(38)).AND.(KVC /= 0)) &
     .OR.((DSQRT(DEV(N)) < DOPTN(38)).AND.(KVC == 0))) THEN
!    IF (DSQRT(DEV(N)) < DOPTN(38)) THEN
      NCONVS=NCONVS+1
      IF (DSQRT(DEV(N)) > DOPTN(38)*100.0D0) &
      WRITE(6,'(A,I3,A,F20.15)') '***** WARNING : RESIDUAL FOR ROOT No.',N,' = ',DSQRT(DEV(N))
     ELSE
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         IF ((DOPTN(24) == 0.0D0).OR.(LAPPROX)) THEN
          RX(L,K)=RX(L,K)/(EPSILON(MOA,K)-EPSILON(MOI,K)-OMEGA(2*NTRIALS-N+1))
          RY(L,K)=RY(L,K)/(EPSILON(MOA,K)-EPSILON(MOI,K)+OMEGA(2*NTRIALS-N+1))
         ELSE IF (LOPTN(32)) THEN
          RX(L,K)=RX(L,K)/(DEPSILON(MOA,K)-DEPSILON(MOI,K)-OMEGA(2*NTRIALS-N+1))
          RY(L,K)=RY(L,K)/(DEPSILON(MOA,K)-DEPSILON(MOI,K)+OMEGA(2*NTRIALS-N+1))
         ELSE
          RX(L,K)=RX(L,K)/(QEPSILON(MOA,K)-QEPSILON(MOI,K)-OMEGA(2*NTRIALS-N+1))
          RY(L,K)=RY(L,K)/(QEPSILON(MOA,K)-QEPSILON(MOI,K)+OMEGA(2*NTRIALS-N+1))
         ENDIF
        ENDDO
       ENDDO
      ENDDO
      S=0.0D0
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        S=S+DCONJG(RX(L,K))*RX(L,K)+DCONJG(RY(L,K))*RY(L,K)
       ENDDO
      ENDDO
      IF (S == 0.0D0) CALL PABORT('ZERO RESIDUAL VECTOR')
      S=DSQRT(S)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        RX(L,K)=RX(L,K)/S
        RY(L,K)=RY(L,K)/S
       ENDDO
      ENDDO
      WRITE(38) RX,RY
     ENDIF
     PREV_OMEGA(N)=OMEGA(2*NTRIALS-N+1)
    ENDDO

    ! CHECK CONVERGENCE AND EXPAND SUBSPACE
    CALL PCPU_TIME(ICPUE)
    WRITE(6,'(I3,I9,I13,I11,7X,F9.7,3X,F9.7,F12.1)') ITR,NTRIALS,NROOTS-NCONVS,NCONVS,MAXENG,MAXVEC,ICPUE-ICPUS
    IF (NCONVS == NROOTS) THEN
     WRITE(6,'(A)') '----------------------------------------------------------------------------'
     IF (IFLAG == 1) THEN
      WRITE(6,'(A)') 'SINGLET ROOTS CONVERGED'
     ELSE IF (IFLAG == 2) THEN
      WRITE(6,'(A)') 'TRIPLET ROOTS CONVERGED'
     ENDIF
     DO N=1,NROOTS
      WRITE(6,'(I9,F10.5,A,F10.5,A)') N,OMEGA(2*NTRIALS-N+1),' HARTREE (',OMEGA(2*NTRIALS-N+1)*EV,' EV )'
      REWIND(36)
      XP=DCMPLX(0.0D0,0.0D0)
      YP=DCMPLX(0.0D0,0.0D0)
      DO M=1,NTRIALS
       READ(36) X,Y
       DO K=-KVC,MAX(0,KVC-1)
        DO L=1,NOV
         XP(L,K)=XP(L,K)+WC(M,2*NTRIALS-N+1)*X(L,K)+WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(Y(L,K))
         YP(L,K)=YP(L,K)+WC(M,2*NTRIALS-N+1)*Y(L,K)+WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(X(L,K))
        ENDDO
       ENDDO
      ENDDO
      S=0.0D0
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        S=S+DCONJG(XP(L,K))*XP(L,K)+DCONJG(YP(L,K))*YP(L,K)
       ENDDO
      ENDDO
      IF (S == 0.0D0) CALL PABORT('ZERO SOLUTION VECTOR')
      S=DSQRT(S)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        XP(L,K)=XP(L,K)/S
        YP(L,K)=YP(L,K)/S
       ENDDO
      ENDDO
! DUMP TRIAL DENSITY MATRIX ASSOCIATED WITH THE CONVGD CIS SOLUTION VECTORS, COMMENT OUT FROM HERE ...
!     ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1))
!     DO I=1,NCGS
!      DO J=1,NCGS
!       DO Q=-CEL1,CEL1
!        P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
!        DO K=-KVC,MAX(0,KVC-1)
!         L=0
!         DO MOI=ICORE+1,IOCC
!          DO MOA=IOCC+1,IALL(K)-IVIRTCORE
!           L=L+1
!           P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*XP(L,K)*PHASE(K*Q)
!          ENDDO
!         ENDDO
!         IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
!        ENDDO
!        P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
!       ENDDO
!      ENDDO
!     ENDDO
!     WRITE(6,'(A,I3)') 'THE TRIAL DENSITY MATRIX FOR THE CONVERGED ROOT ',N
!     CALL DUMP9(P_AX,NCGS,CEL1)
!     DEALLOCATE(P_AX)
! ... TO HERE
      IF (IOPTN(9) >= 2) THEN
       WRITE(6,'(A,I3)') 'X-COMPONENT OF THE ROOT ',N
       CALL DUMP8(XP,NOV,KVC)
       WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE ROOT ',N
       CALL DUMP8(YP,NOV,KVC)
      ENDIF
      IF (LAPPROX) THEN
       CISQ=0.0D0
       CISD=0.0D0
       DO K=-KVC,MAX(0,KVC-1)
        L=0
        DO MOI=ICORE+1,IOCC
         DO MOA=IOCC+1,IALL(K)-IVIRTCORE
          L=L+1
          IF (LOPTN(32)) THEN
           IF ((MOA <= IOCC+IOPTN(44)).AND.(MOI >= IOCC+1-IOPTN(44))) &
           CISD=CISD+(DEPSILON(MOA,K)-DEPSILON(MOI,K)-EPSILON(MOA,K)+EPSILON(MOI,K))*(CDABS(XP(L,K))**2+CDABS(YP(L,K))**2)
          ENDIF
          IF ((MOA <= IOCC+IOPTN(44)).AND.(MOI >= IOCC+1-IOPTN(44))) &
          CISQ=CISQ+(QEPSILON(MOA,K)-QEPSILON(MOI,K)-EPSILON(MOA,K)+EPSILON(MOI,K))*(CDABS(XP(L,K))**2+CDABS(YP(L,K))**2)
         ENDDO
        ENDDO
       ENDDO
       IF (.NOT.LTDR) WRITE(6,'(A,F10.5,A,F10.5,A)') 'CIS-MP2  ', &
       OMEGA(2*NTRIALS-N+1)+CISQ,' HARTREE (',(OMEGA(2*NTRIALS-N+1)+CISQ)*EV,' EV )'
       IF ((.NOT.LTDR).AND.(LOPTN(32))) WRITE(6,'(A,F10.5,A,F10.5,A)') 'CIS-DYSON', &
       OMEGA(2*NTRIALS-N+1)+CISD,' HARTREE (',(OMEGA(2*NTRIALS-N+1)+CISD)*EV,' EV )'
      ENDIF
      S=0.0D0
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         S=S+CDABS(XP(L,K))**2+CDABS(YP(L,K))**2
         IF (KVC == 0) THEN
          IF (CDABS(XP(L,K)) > 0.01D0) WRITE(6,'(A,I3,A,I3,A,F15.10)') '   X --- V(',MOA,') - O(',MOI,') ',CDABS(XP(L,K))**2
          IF (CDABS(YP(L,K)) > 0.01D0) WRITE(6,'(A,I3,A,I3,A,F15.10)') '   Y --- V(',MOA,') - O(',MOI,') ',CDABS(YP(L,K))**2
         ELSE
          IF (CDABS(XP(L,K)) > 0.01D0) &
          WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F15.10)') '   X --- V(',MOA,'[',K,']) - O(',MOI,'[',K,']) ',CDABS(XP(L,K))**2
          IF (CDABS(YP(L,K)) > 0.01D0) &
          WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F15.10)') '   Y --- V(',MOA,'[',K,']) - O(',MOI,'[',K,']) ',CDABS(YP(L,K))**2
         ENDIF
        ENDDO
       ENDDO
      ENDDO
      IF (IOPTN(9) >= 2) WRITE(6,'(A,F15.10)') 'NORM OF THE SOLUTION VECTOR =',S
     ENDDO
     DEALLOCATE(A,B,C,D,WA,WC,WX,WE,OMEGA,RX,RY,PREV_OMEGA)
     DEALLOCATE(X,Y,XP,YP,PHASE,DEV,LDONE)
     RETURN
    ELSE
     REWIND(38)
     NDISCARDS=0
     DO N=1,NROOTS-NCONVS
      READ(38) RX,RY

      ! CHECK ORTHOGONALITY
      ALLOCATE(UNITC(2*NTRIALS+2,2*NTRIALS+2),UNITE(2*NTRIALS+2),UNITX(2*NTRIALS+2,2*NTRIALS+2))
      UNITC(1:2*NTRIALS+2,1:2*NTRIALS+2)=DCMPLX(0.0D0,0.0D0)
      DO O=1,NTRIALS+1
       IF (O == NTRIALS+1) THEN
        XP=RX
        YP=RY
       ELSE
        REWIND(36)
        DO I=1,O
         READ(36) XP,YP
        ENDDO
       ENDIF
       REWIND(36)
       DO M=1,NTRIALS+1
        IF (M == NTRIALS+1) THEN
         X=RX
         Y=RY
        ELSE
         READ(36) X,Y
        ENDIF
        DO K=-KVC,MAX(0,KVC-1)
         DO L=1,NOV
          UNITC(M,          O)=UNITC(M,          O)+DCONJG(X(L,K))*XP(L,K)        +DCONJG(Y(L,K))*YP(L,K)
          UNITC(M+NTRIALS+1,O)=UNITC(M+NTRIALS+1,O)+DCONJG(X(L,K))*DCONJG(YP(L,K))+DCONJG(Y(L,K))*DCONJG(XP(L,K))
          UNITC(M,          O+NTRIALS+1)=UNITC(M,          O+NTRIALS+1)+Y(L,K)*XP(L,K)                +X(L,K)*YP(L,K)
          UNITC(M+NTRIALS+1,O+NTRIALS+1)=UNITC(M+NTRIALS+1,O+NTRIALS+1)+Y(L,K)*DCONJG(YP(L,K))        +X(L,K)*DCONJG(XP(L,K))
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      CALL HHBS(2*NTRIALS+2,2*NTRIALS+2,UNITC,UNITE,UNITX)
      IF (IOPTN(9) >= 2) WRITE(6,'(A,F20.14)') 'SMALLEST METRIC = ',UNITE(1)
      IF (UNITE(1) < 1.0D-9) THEN
       NDISCARDS=NDISCARDS+1
       DEALLOCATE(UNITC,UNITE,UNITX)
       CYCLE
      ENDIF
      DEALLOCATE(UNITC,UNITE,UNITX)

      REWIND(36)
      DO M=1,NTRIALS
       READ(36) X,Y
      ENDDO
      WRITE(36) RX,RY
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'X-COMPONENT OF THE NEW TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(RX,NOV,KVC)
       WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE NEW TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(RY,NOV,KVC)
      ENDIF
      NTRIALS=NTRIALS+1
      IF (NTRIALS > MAXTRIALS) CALL PABORT('NUMBER OF TRIAL VECTORS EXCEEDED MAXIMUM ALLOWED VALUE')
     ENDDO
     IF (NDISCARDS > 0) WRITE(6,'(A,I2,A)') &
     '***** WARNING : ',NDISCARDS,' RESIDUAL VECTOR(S) HAVE BEEN DISCARDED TO AVOID LINEAR DEPENDENCE'
     IF (NDISCARDS == NROOTS-NCONVS) THEN
      WRITE(6,'(A)') '----------------------------------------------------------------------------'
      CALL WARNING('THE ALGORITHM FAILED TO EXTEND SUBSPACE')
      IF (IFLAG == 1) THEN
       WRITE(6,'(A)') 'SINGLET ROOTS'
      ELSE IF (IFLAG == 2) THEN
       WRITE(6,'(A)') 'TRIPLET ROOTS'
      ENDIF
      DO N=1,NROOTS
       WRITE(6,'(I9,F10.5,A,F10.5,A)') N,OMEGA(2*NTRIALS-N+1),' HARTREE (',OMEGA(2*NTRIALS-N+1)*EV,' EV )'
       REWIND(36)
       XP=DCMPLX(0.0D0,0.0D0)
       YP=DCMPLX(0.0D0,0.0D0)
       DO M=1,NTRIALS
        READ(36) X,Y
        DO K=-KVC,MAX(0,KVC-1)
         DO L=1,NOV
          XP(L,K)=XP(L,K)+WC(M,2*NTRIALS-N+1)*X(L,K)+WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(Y(L,K))
          YP(L,K)=YP(L,K)+WC(M,2*NTRIALS-N+1)*Y(L,K)+WC(M+NTRIALS,2*NTRIALS-N+1)*DCONJG(X(L,K))
         ENDDO
        ENDDO
       ENDDO
       S=0.0D0
       DO K=-KVC,MAX(0,KVC-1)
        DO L=1,NOV
         S=S+DCONJG(XP(L,K))*XP(L,K)+DCONJG(YP(L,K))*YP(L,K)
        ENDDO
       ENDDO
       IF (S == 0.0D0) CALL PABORT('ZERO SOLUTION VECTOR')
       S=DSQRT(S)
       DO K=-KVC,MAX(0,KVC-1)
        DO L=1,NOV
         XP(L,K)=XP(L,K)/S
         YP(L,K)=YP(L,K)/S
        ENDDO
       ENDDO
! DUMP TRIAL DENSITY MATRIX ASSOCIATED WITH THE CONVGD CIS SOLUTION VECTORS, COMMENT OUT FROM HERE ...
!      ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1))
!      DO I=1,NCGS
!       DO J=1,NCGS
!        DO Q=-CEL1,CEL1
!         P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
!         DO K=-KVC,MAX(0,KVC-1)
!          L=0
!          DO MOI=ICORE+1,IOCC
!           DO MOA=IOCC+1,IALL(K)-IVIRTCORE
!            L=L+1
!            P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*XP(L,K)*PHASE(K*Q)
!           ENDDO
!          ENDDO
!          IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
!         ENDDO
!         P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
!        ENDDO
!       ENDDO
!      ENDDO
!      WRITE(6,'(A,I3)') 'THE TRIAL DENSITY MATRIX FOR THE CONVERGED ROOT ',N
!      CALL DUMP9(P_AX,NCGS,CEL1)
!      DEALLOCATE(P_AX)
! ... TO HERE
       IF (IOPTN(9) >= 2) THEN
        WRITE(6,'(A,I3)') 'X-COMPONENT OF THE ROOT ',N
        CALL DUMP8(XP,NOV,KVC)
        WRITE(6,'(A,I3)') 'Y-COMPONENT OF THE ROOT ',N
        CALL DUMP8(YP,NOV,KVC)
       ENDIF
       IF (LAPPROX) THEN
        CISQ=0.0D0
        CISD=0.0D0
        DO K=-KVC,MAX(0,KVC-1)
         L=0
         DO MOI=ICORE+1,IOCC
          DO MOA=IOCC+1,IALL(K)-IVIRTCORE
           L=L+1
           IF (LOPTN(32)) THEN
            IF ((MOA <= IOCC+IOPTN(44)).AND.(MOI >= IOCC+1-IOPTN(44))) &
            CISD=CISD+(DEPSILON(MOA,K)-DEPSILON(MOI,K)-EPSILON(MOA,K)+EPSILON(MOI,K))*(CDABS(XP(L,K))**2+CDABS(YP(L,K))**2)
           ENDIF
           IF ((MOA <= IOCC+IOPTN(44)).AND.(MOI >= IOCC+1-IOPTN(44))) &
           CISQ=CISQ+(QEPSILON(MOA,K)-QEPSILON(MOI,K)-EPSILON(MOA,K)+EPSILON(MOI,K))*(CDABS(XP(L,K))**2+CDABS(YP(L,K))**2)
          ENDDO
         ENDDO
        ENDDO
        IF (.NOT.LTDR) WRITE(6,'(A,F10.5,A,F10.5,A)') 'CIS-MP2  ', &
        OMEGA(2*NTRIALS-N+1)+CISQ,' HARTREE (',(OMEGA(2*NTRIALS-N+1)+CISQ)*EV,' EV )'
        IF ((.NOT.LTDR).AND.(LOPTN(32))) WRITE(6,'(A,F10.5,A,F10.5,A)') 'CIS-DYSON', &
        OMEGA(2*NTRIALS-N+1)+CISD,' HARTREE (',(OMEGA(2*NTRIALS-N+1)+CISD)*EV,' EV )'
       ENDIF
       S=0.0D0
       DO K=-KVC,MAX(0,KVC-1)
        L=0
        DO MOI=ICORE+1,IOCC
         DO MOA=IOCC+1,IALL(K)-IVIRTCORE
          L=L+1
          S=S+CDABS(XP(L,K))**2+CDABS(YP(L,K))**2
          IF (KVC == 0) THEN
           IF (CDABS(XP(L,K)) > 0.01D0) WRITE(6,'(A,I3,A,I3,A,F15.10)') '   X --- V(',MOA,') - O(',MOI,') ',CDABS(XP(L,K))**2
           IF (CDABS(YP(L,K)) > 0.01D0) WRITE(6,'(A,I3,A,I3,A,F15.10)') '   Y --- V(',MOA,') - O(',MOI,') ',CDABS(YP(L,K))**2
          ELSE
           IF (CDABS(XP(L,K)) > 0.01D0) &
           WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F15.10)') '   X --- V(',MOA,'[',K,']) - O(',MOI,'[',K,']) ',CDABS(XP(L,K))**2
           IF (CDABS(YP(L,K)) > 0.01D0) &
           WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F15.10)') '   Y --- V(',MOA,'[',K,']) - O(',MOI,'[',K,']) ',CDABS(YP(L,K))**2
          ENDIF
         ENDDO
        ENDDO
       ENDDO
       IF (IOPTN(9) >= 2) WRITE(6,'(A,F15.10)') 'NORM OF THE SOLUTION VECTOR =',S
      ENDDO
      DEALLOCATE(A,B,C,D,WA,WC,WX,WE,OMEGA,RX,RY,PREV_OMEGA)
      DEALLOCATE(X,Y,XP,YP,PHASE,DEV,LDONE)
      RETURN
     ENDIF
     IF (NTRIALS >= NOV*MAX(1,2*KVC)) CALL WARNING('TRIAL VECTORS HAVE ALREADY SPANNED THE WHOLE SPACE')
    ENDIF

    DEALLOCATE(A,B,C,D,WA,WC,WX,WE,OMEGA,RX,RY)
    IF (ITR > MAXITR) CALL PABORT('NUMBER OF ITERATIONS EXCEEDED MAXIMUM ALLOWED VALUE')

   ENDDO
END SUBROUTINE



SUBROUTINE GUESS_CISTRIALVECTOR
! CONSTRUCT INITIAL TRIAL VECTORS FOR CIS CALCULATIONS AND STORE THEM ON DISK.

   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE CIS

   IMPLICIT NONE
   INTEGER :: I,J,K,L
   DOUBLE PRECISION :: A
   LOGICAL :: LDONE
   INTEGER,ALLOCATABLE :: SORT(:)
   DOUBLE PRECISION,ALLOCATABLE :: H(:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: X(:,:)

   NOV=(IOCC-ICORE)*(IALLMAX-IOCC-IVIRTCORE)
   WRITE(6,'(A)') 'CALCULATE EXCITATION ENERGIES BY CONFIGURATION INTERACTION SINGLES THEORY'
   NROOTS=IOPTN(36)
   IF (NROOTS > NOV*MAX(1,2*KVC)) THEN
    CALL WARNING('NUMBER OF ROOTS HAS BEEN REDUCED')
    NROOTS=NOV*MAX(1,2*KVC)
   ENDIF
   WRITE(6,'(A,I3)') 'NUMBER OF ROOTS REQUESTED       = ',NROOTS
   MAXTRIALS=MIN(INT(DOPTN(26)*1000000.0D0/(DFLOAT(NOV*MAX(1,2*KVC))*32.0D0)),NOV*MAX(1,2*KVC))
   IF (MAXTRIALS < NROOTS) CALL PABORT('INSUFFICIENT DISKSPACE')
   WRITE(6,'(A,I3)') 'MAXIMUM NUMBER OF TRIAL VECTORS = ',MAXTRIALS

   ALLOCATE(SORT(NOV*MAX(1,2*KVC)),H(NOV,-KVC:KVC),X(NOV,-KVC:KVC))

   ! CALCULATE ONE-ELECTRON ENERGY DIFFERENCES
   DO K=-KVC,MAX(0,KVC-1)
    L=0
    DO I=ICORE+1,IOCC
     DO J=IOCC+1,IALL(K)-IVIRTCORE
      L=L+1
      H(L,K)=EPSILON(J,K)-EPSILON(I,K)
      IF (IOPTN(9) >= 2) WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F10.4)') ' V(',J,'[',K,']) - O(',I,'[',K,']) = ',H(L,K)
     ENDDO
    ENDDO
    IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
   ENDDO

   ! SORT THE ONE-ELECTRON ENERGY DIFFERENCES
   DO I=1,NOV*MAX(1,2*KVC)
    A=1.0D99
    DO K=-KVC,MAX(0,KVC-1)
     DO L=1,NOV
      IF (H(L,K) < A) THEN
       IF (I == 1) THEN
        A=H(L,K)
        SORT(I)=(L-1)+(K+KVC)*NOV
       ELSE
        LDONE=.FALSE.
        DO J=1,I-1
         IF (SORT(J) == (L-1)+(K+KVC)*NOV) LDONE=.TRUE.
        ENDDO
        IF (.NOT.LDONE) THEN
         A=H(L,K)
         SORT(I)=(L-1)+(K+KVC)*NOV
        ENDIF
       ENDIF
      ENDIF
     ENDDO
    ENDDO
   ENDDO
   IF (IOPTN(9) == 3) THEN
    WRITE(6,'(A)') 'EXCITATION ENERGIES IN ASCENDING ORDER'
    DO I=1,NOV*MAX(1,2*KVC)
     WRITE(6,'(I3,F10.4)') I,H(MOD(SORT(I),NOV)+1,SORT(I)/NOV-KVC)
    ENDDO
   ENDIF

   ! INCREASE THE NUMBER OF ROOTS IF NECESSARY
   J=NROOTS
   IF (NROOTS < NOV*MAX(1,2*KVC)) THEN
    DO I=NROOTS+1,NOV*MAX(1,2*KVC)
     IF (H(MOD(SORT(I),NOV)+1,SORT(I)/NOV-KVC)-H(MOD(SORT(NROOTS),NOV)+1,SORT(NROOTS)/NOV-KVC) < THRESH) J=J+1
    ENDDO
   ENDIF
   IF (J > NROOTS) THEN
    NROOTS=J
    WRITE(6,'(A,I3)') 'NUMBER OF ROOTS HAS BEEN INCREASED TO ',NROOTS
   ENDIF

   ! CONSTRUCT INITIAL TRIAL VECTORS AND STORE
   REWIND(36)
   DO I=1,NROOTS
    X(1:NOV,-KVC:KVC)=DCMPLX(0.0D0,0.0D0)
    X(MOD(SORT(I),NOV)+1,SORT(I)/NOV-KVC)=DCMPLX(1.0D0,0.0D0)
    WRITE(36) X
   ENDDO

   NTRIALS=NROOTS

   DEALLOCATE(SORT,H,X)
END SUBROUTINE



SUBROUTINE DAVIDSON_DIAGONALIZATION(IFLAG)
! DIRECT DAVIDSON'S ALGORITHMS FOR SOLVING LARGE HERMITIAN CIS EIGENVALUE PROBLEMS.
! IFLAG = 1 : SINGLET EXCITATION ENERGY CALCULATIONS.
! IFLAG = 2 : TRIPLET EXCITATION ENERGY CALCULATIONS.

   USE CONSTANTS
   USE CONTROL
   USE STRUCTURE
   USE BASISSET
   USE INTEGRAL
   USE DFT
   USE CIS

   IMPLICIT NONE
   REAL :: ICPUS,ICPUE
   INTEGER :: IFLAG
   INTEGER :: I,J,K,L,M,N,O,Q,MOI,MOA,ITR,NDISCARDS
   DOUBLE PRECISION :: S,MAXVEC,MAXENG
   DOUBLE COMPLEX :: T
   DOUBLE COMPLEX,ALLOCATABLE :: X(:,:)                      ! TRIAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: XP(:,:)                     ! PRODUCT VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: RX(:,:)                     ! RESIDUAL VECTORS
   DOUBLE COMPLEX,ALLOCATABLE :: P_AX(:,:,:)                 ! TRIAL DENSITY MATRIX FOR ONE TRIAL VECTOR
   DOUBLE COMPLEX,ALLOCATABLE :: A_X(:,:,:)                  ! PRODUCT DENSITY MATRICES
   DOUBLE COMPLEX,ALLOCATABLE :: DUMMY(:,:,:)
   DOUBLE COMPLEX,ALLOCATABLE :: A(:,:),C(:,:)               ! SUBSPACE REPRESENTATION FOR A,B, AND METRIC MATRICES
   DOUBLE COMPLEX,ALLOCATABLE :: WX(:,:)                     ! SUBSPACE REPRESENTATION WITH THE DOUBLE DIMENSION
   DOUBLE COMPLEX,ALLOCATABLE :: UNITC(:,:),UNITX(:,:)       ! SUBSPACE REPRESENTATION FOR EUCLIDEAN NORM
   DOUBLE COMPLEX,ALLOCATABLE :: PHASE(:)
   DOUBLE PRECISION,ALLOCATABLE :: OMEGA(:),DEV(:)           ! EIGENVALUES OF SUBSPACE EIGENEQUATION,SQUARED NORM OF RESIDUAL
   DOUBLE PRECISION,ALLOCATABLE :: UNITE(:)                  ! METRIC OF EUCLIDEAN NORM
   DOUBLE PRECISION,ALLOCATABLE :: PREV_OMEGA(:)             ! EIGENVALUES OF THE PREVIOUS ITERATION
   LOGICAL,ALLOCATABLE :: LDONE(:)                           ! .TRUE. WHEN PRODUCT FORMATION HAS ALREADY BEEN DONE

   ALLOCATE(PHASE(-CEL1*KVC:CEL1*KVC))
   ALLOCATE(DEV(NROOTS),LDONE(MAXTRIALS),PREV_OMEGA(NROOTS))
   ALLOCATE(X(NOV,-KVC:KVC))
   ALLOCATE(XP(NOV,-KVC:KVC))

   IF (IFLAG == 1) THEN
    WRITE(6,'(A)') 'CALCULATE SINGLET EXCITATION ENERGIES BY DAVIDSON ALGORITHM'
   ELSE IF (IFLAG == 2) THEN
    WRITE(6,'(A)') 'CALCULATE TRIPLET EXCITATION ENERGIES BY DAVIDSON ALGORITHM'
   ENDIF
   WRITE(6,'(A)') '----------------------------------------------------------------------------'
   WRITE(6,'(A)') 'ITR  TRIAL VECTORS  RTS LEFT  RTS CNVRGD   ENERGYDEV   VECTORDEV   CPU / SEC'

   ! PRECALCULATE PHASE FACTORS
   DO I=-CEL1*KVC,CEL1*KVC
    IF (KVC == 0) THEN
     PHASE(I)=DCMPLX(1.0D0,0.0D0)
    ELSE
     PHASE(I)=DCMPLX(DCOS(DFLOAT(I)/DFLOAT(KVC)*PI),DSIN(DFLOAT(I)/DFLOAT(KVC)*PI))
    ENDIF
   ENDDO
   LDONE(1:MAXTRIALS)=.FALSE.
   PREV_OMEGA(1:NROOTS)=9.9D99
   ITR=0

   ! MAIN LOOP
   DO
    ITR=ITR+1
    REWIND(36)
    REWIND(37)

    ! LOOP OVER TRIAL VECTORS

    ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1))
    ALLOCATE(A_X(NCGS,NCGS,-CEL1:CEL1))

    CALL PCPU_TIME(ICPUS)
    DO N=1,NTRIALS

     ! IF THE PRODUCT FORMATION HAS ALREADY BEEN DONE, JUST PROCEED
     IF (LDONE(N)) THEN
      READ(36) X
      READ(37) X
     ELSE
      LDONE(N)=.TRUE.
      ! TRANSFORM TRIAL VECTORS TO TRIAL DENSITY MATRICES
      READ(36) X
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'THE TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(X,NOV,KVC)
      ENDIF
      DO I=1,NCGS
       DO J=1,NCGS
        DO Q=-CEL1,CEL1
         P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
         DO K=-KVC,MAX(0,KVC-1)
          L=0
          DO MOI=ICORE+1,IOCC
           DO MOA=IOCC+1,IALL(K)-IVIRTCORE
            L=L+1
            P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*X(L,K)*PHASE(K*Q)
           ENDDO
          ENDDO
          IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
         ENDDO
         P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
        ENDDO
       ENDDO
      ENDDO
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'THE TRIAL DENSITY MATRIX FOR ROOT ',N
       CALL DUMP9(P_AX,NCGS,CEL1)
      ENDIF

      ! ZERO SCRATCH PRODUCT MATRICES
      A_X(1:NCGS,1:NCGS,-CEL1:CEL1)=DCMPLX(0.0D0,0.0D0)

      ! CONTRACT TRIAL DENSITY MATRIX WITH OEP KERNEL
      IF (DOPTN(84) /= 0.0D0) CALL CONTRACT_OEP(A_X,P_AX,NCGS,CEL1,IFLAG)

      ! CONTRACT TRIAL DENSITY MATRIX WITH TWO-ELECTRON INTEGRALS
      IF (LOPTN(25)) THEN
       ALLOCATE(DUMMY(NCGS,NCGS,-CEL1:CEL1))
       CALL DIRECT_CONTRACT_FOURINDEX_ERI(.FALSE.,A_X,P_AX,DUMMY,DUMMY,DUMMY,DUMMY,DUMMY,DUMMY,NCGS,CEL1,IFLAG)
       DEALLOCATE(DUMMY)
      ELSE
       CALL CONTRACT_FOURINDEX_ERI(A_X,P_AX,NCGS,CEL1,IFLAG)
      ENDIF

      ! CONTRACT TRIAL DENSITY MATRICES WITH EXCHANGE-CORRELATION INTEGRALS
      IF (LDFT) THEN
       D2F1=0.0D0
       IF (LGC) THEN
        D1F2=0.0D0
        D2F2=0.0D0
        D2F3=0.0D0
       ENDIF
      ENDIF
      IF (DOPTN(20) /= 0.0D0) CALL SLATER_EXCHANGE(IFLAG)
      IF (DOPTN(21) /= 0.0D0) CALL VOSKO_WILK_NUSAIR_CORRELATION(IFLAG)
      IF (DOPTN(22) /= 0.0D0) CALL BECKE88_EXCHANGE(IFLAG)
      IF (DOPTN(23) /= 0.0D0) CALL LEE_YANG_PARR_CORRELATION(IFLAG)
      IF (LDFT.AND.(.NOT.(LGC))) THEN
       CALL TRIAL_ELECTRON_DENSITY(P_AX,NCGS,CEL1)
       CALL CONTRACT_LOCAL_XC(A_X,NCGS,CEL1)
      ELSE IF (LDFT.AND.LGC) THEN
       CALL TRIAL_ELECTRON_DENSITY(P_AX,NCGS,CEL1)
       CALL TRIAL_ELECTRON_DENSITY_GRADIENT(P_AX,NCGS,CEL1)
       CALL CONTRACT_GC_XC(A_X,NCGS,CEL1)
      ENDIF
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'A TIMES X FOR ROOT ',N
       CALL DUMP9(A_X,NCGS,CEL1)
      ENDIF
 
      ! BACK-TRANSFORM PRODUCT DENSITY MATRICES TO PRODUCT VECTORS
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         XP(L,K)=DCMPLX(0.0D0,0.0D0)
         DO I=1,NCGS
          DO J=1,NCGS
           DO Q=-CEL1,CEL1
            XP(L,K)=XP(L,K)+DCONJG(CO(I,MOA,K))*CO(J,MOI,K)*A_X(I,J,Q)*PHASE(K*Q)
           ENDDO
          ENDDO
         ENDDO
         ! ADD ONE-ELECTRON ENERGY DIFFERENCES
         XP(L,K)=XP(L,K)+(EPSILON(MOA,K)-EPSILON(MOI,K))*X(L,K)
        ENDDO
       ENDDO
      ENDDO

      ! STORE THE PRODUCT VECTORS
      WRITE(37) XP
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'THE PRODUCT VECTOR FOR ROOT ',N
       CALL DUMP8(XP,NOV,KVC)
      ENDIF

     ENDIF
    ENDDO

    DEALLOCATE(P_AX,A_X)
    ALLOCATE(A(NTRIALS,NTRIALS),C(NTRIALS,NTRIALS))
    ALLOCATE(WX(NTRIALS,NTRIALS))
    ALLOCATE(OMEGA(NTRIALS))

    ! FORM A MATRIX IN THE SUBSPACE SPANNED BY THE TRIAL VECTORS
    REWIND(37)
    ! LOOP OVER PRODUCT VECTORS
    DO N=1,NTRIALS
     READ(37) XP
     REWIND(36)
     ! LOOP OVER TRIAL VECTORS
     DO M=1,NTRIALS
      READ(36) X
      A(M,N)=DCMPLX(0.0D0,0.0D0)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        A(M,N)=A(M,N)+DCONJG(X(L,K))*XP(L,K)
       ENDDO
      ENDDO
     ENDDO
    ENDDO

    ! FORM C MATRIX IN THE SUBSPACE SPANNED BY THE TRIAL VECTORS
    ! LOOP OVER TRIAL VECTORS
    DO N=1,NTRIALS
     REWIND(36)
     DO I=1,N
      READ(36) XP
     ENDDO
     REWIND(36)
     ! LOOP OVER TRIAL VECTORS
     DO M=1,NTRIALS
      READ(36) X
      C(M,N)=DCMPLX(0.0D0,0.0D0)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        C(M,N)=C(M,N)+DCONJG(X(L,K))*XP(L,K) ! HERE XP IS ACTUALLY X
       ENDDO
      ENDDO
     ENDDO
    ENDDO
    IF (IOPTN(9) >= 2) THEN
     WRITE(6,'(A)') 'A MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(A,NTRIALS)
     WRITE(6,'(A)') 'C MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(C,NTRIALS)
    ENDIF

    S=0.0D0
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      IF (CDABS(DCONJG(A(M,N))-A(N,M)) > S) S=CDABS(DCONJG(A(M,N))-A(N,M))
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) WRITE(6,'(A,F20.15)') 'DEVIATION FROM HERMITIAN A = ',S
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      T=(DCONJG(A(M,N))+A(N,M))/2.0D0
      A(M,N)=DCONJG(T)
      A(N,M)=T
     ENDDO
    ENDDO
    S=0.0D0
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      IF (CDABS(DCONJG(C(M,N))-C(N,M)) > S) S=CDABS(DCONJG(C(M,N))-C(N,M))
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) WRITE(6,'(A,F20.15)') 'DEVIATION FROM HERMITIAN C = ',S
    DO N=1,NTRIALS
     DO M=1,NTRIALS
      T=(DCONJG(C(M,N))+C(N,M))/2.0D0
      C(M,N)=DCONJG(T)
      C(N,M)=T
     ENDDO
    ENDDO
    IF (IOPTN(9) == 3) THEN
     WRITE(6,'(A)') 'SYMMETRIZED A MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(A,NTRIALS)
     WRITE(6,'(A)') 'SYMMETRIZED C MATRIX IN THE SUBSPACE REPRESENTATION'
     CALL DUMP7(C,NTRIALS)
    ENDIF

    ! CANONICAL ORTHOGONALIZATION OF C MATRIX
    CALL HHBS(NTRIALS,NTRIALS,C,OMEGA,WX)
    IF (IOPTN(9) >= 2) WRITE(6,'(A,100F10.5:)') 'METRIC  = ',(OMEGA(J),J=1,NTRIALS)
    DO J=1,NTRIALS
     IF (OMEGA(J) <= 0.0D0) CALL PABORT('METRIC IS NOT POSITIVE DEFINITE')
     DO K=1,NTRIALS
      C(K,J)=WX(K,J)/DSQRT(OMEGA(J))
     ENDDO
    ENDDO

    ! DIAGONALIZATION OF A MATRIX
    DO J=1,NTRIALS
     DO K=1,NTRIALS
      WX(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,NTRIALS
       WX(K,J)=WX(K,J)+A(K,L)*C(L,J)
      ENDDO
     ENDDO
    ENDDO
    DO J=1,NTRIALS
     DO K=1,NTRIALS
      A(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,NTRIALS
       A(K,J)=A(K,J)+DCONJG(C(L,K))*WX(L,J)
      ENDDO
     ENDDO
    ENDDO
    CALL HHBS(NTRIALS,NTRIALS,A,OMEGA,WX)
    IF (IOPTN(9) >= 2) WRITE(6,'(A,100F10.5:)') 'EIGENVALUES = ',(OMEGA(J),J=1,NTRIALS)
    DO J=1,NTRIALS
     DO K=1,NTRIALS
      A(K,J)=DCMPLX(0.0D0,0.0D0)
      DO L=1,NTRIALS
       A(K,J)=A(K,J)+C(K,L)*WX(L,J)
      ENDDO
     ENDDO
     IF (IOPTN(9) == 3) THEN
      WRITE(6,'(A,I3,A,F10.6,A,F10.4,A)') 'ROOT No.',J,' EXCITATION ENERGY / HARTREE = ',OMEGA(J),' ( ',OMEGA(J)*EV,' )'
      DO K=1,NTRIALS
       WRITE(6,'(A,I3,A,2F8.4)') '  TRIAL VECTOR No.',K,'  TIMES ',A(K,J)
      ENDDO
     ENDIF
    ENDDO

    ALLOCATE(RX(NOV,-KVC:KVC))

    ! CALCULATE RESIDUAL VECTOR FOR EACH ROOT
    MAXVEC=0.0D0
    MAXENG=0.0D0
    NCONVS=0
    REWIND(38)
    DO N=1,NROOTS
     REWIND(36)
     REWIND(37)
     RX(1:NOV,-KVC:KVC)=DCMPLX(0.0D0,0.0D0)
     DO M=1,NTRIALS
      READ(36) X
      READ(37) XP
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        RX(L,K)=RX(L,K)+A(M,N)*XP(L,K)-A(M,N)*X(L,K)*OMEGA(N)
       ENDDO
      ENDDO
     ENDDO
     DEV(N)=0.0D0
     DO K=-KVC,MAX(0,KVC-1)
      DO L=1,NOV
       DEV(N)=DEV(N)+DCONJG(RX(L,K))*RX(L,K)
      ENDDO
     ENDDO
     IF (IOPTN(9) == 3) WRITE(6,'(A,I3,A,F20.15)') 'ROOT No.',N,' NORM OF THE RESIDUAL = ',DSQRT(DEV(N))
     IF (DSQRT(DEV(N)) > MAXVEC) MAXVEC=DSQRT(DEV(N))
     IF (DABS(OMEGA(N)-PREV_OMEGA(N)) > MAXENG) MAXENG=DABS(OMEGA(N)-PREV_OMEGA(N))
     IF (IOPTN(9) == 3) THEN
      WRITE(6,'(A,I3)') 'THE RESIDUAL VECTOR FOR ROOT ',N
      CALL DUMP8(RX,NOV,KVC)
     ENDIF
!    IF ((DSQRT(DEV(N)) < DOPTN(38)).AND.(DABS(OMEGA(N)-PREV_OMEGA(N)) < DOPTN(38)/10.0D0)) THEN
!    IF ((DABS(OMEGA(N)-PREV_OMEGA(N)) < DOPTN(38)).OR.((DSQRT(DEV(N)) < DOPTN(38)).AND.(KVC == 0))) THEN
     IF (((DABS(OMEGA(N)-PREV_OMEGA(N)) < DOPTN(38)).AND.(KVC /= 0)) &
     .OR.((DSQRT(DEV(N)) < DOPTN(38)).AND.(KVC == 0))) THEN
!    IF (DSQRT(DEV(N)) < DOPTN(38)) THEN
      NCONVS=NCONVS+1
      IF (DSQRT(DEV(N)) > DOPTN(38)*100.0D0) &
      WRITE(6,'(A,I3,A,F20.15)') '***** WARNING : RESIDUAL FOR ROOT No.',N,' = ',DSQRT(DEV(N))
     ELSE
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         RX(L,K)=RX(L,K)/(EPSILON(MOA,K)-EPSILON(MOI,K)-OMEGA(N))
        ENDDO
       ENDDO
      ENDDO
      S=0.0D0
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        S=S+DCONJG(RX(L,K))*RX(L,K)
       ENDDO
      ENDDO
      IF (S == 0.0D0) CALL PABORT('ZERO RESIDUAL VECTOR')
      S=DSQRT(S)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        RX(L,K)=RX(L,K)/S
       ENDDO
      ENDDO
      WRITE(38) RX
     ENDIF
     PREV_OMEGA(N)=OMEGA(N)
    ENDDO

    ! CHECK CONVERGENCE AND EXPAND SUBSPACE
    CALL PCPU_TIME(ICPUE)
    WRITE(6,'(I3,I9,I13,I11,7X,F9.7,3X,F9.7,F12.1)') ITR,NTRIALS,NROOTS-NCONVS,NCONVS,MAXENG,MAXVEC,ICPUE-ICPUS
    IF (NCONVS == NROOTS) THEN
     WRITE(6,'(A)') '----------------------------------------------------------------------------'
     IF (IFLAG == 1) THEN
      WRITE(6,'(A)') 'SINGLET ROOTS CONVERGED'
     ELSE IF (IFLAG == 2) THEN
      WRITE(6,'(A)') 'TRIPLET ROOTS CONVERGED'
     ENDIF
     DO N=1,NROOTS
      WRITE(6,'(I9,F10.5,A,F10.5,A)') N,OMEGA(N),' HARTREE (',OMEGA(N)*EV,' EV )'
      REWIND(36)
      XP=DCMPLX(0.0D0,0.0D0)
      DO M=1,NTRIALS
       READ(36) X
       DO K=-KVC,MAX(0,KVC-1)
        DO L=1,NOV
         XP(L,K)=XP(L,K)+A(M,N)*X(L,K)
        ENDDO
       ENDDO
      ENDDO
      S=0.0D0
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        S=S+DCONJG(XP(L,K))*XP(L,K)
       ENDDO
      ENDDO
      IF (S == 0.0D0) CALL PABORT('ZERO SOLUTION VECTOR')
      S=DSQRT(S)
      DO K=-KVC,MAX(0,KVC-1)
       DO L=1,NOV
        XP(L,K)=XP(L,K)/S
       ENDDO
      ENDDO
! DUMP TRIAL DENSITY MATRIX ASSOCIATED WITH THE CONVGD CIS SOLUTION VECTORS, COMMENT OUT FROM HERE ...
!     ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1))
!     DO I=1,NCGS
!      DO J=1,NCGS
!       DO Q=-CEL1,CEL1
!        P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
!        DO K=-KVC,MAX(0,KVC-1)
!         L=0
!         DO MOI=ICORE+1,IOCC
!          DO MOA=IOCC+1,IALL(K)-IVIRTCORE
!           L=L+1
!           P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*XP(L,K)*PHASE(K*Q)
!          ENDDO
!         ENDDO
!         IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
!        ENDDO
!        P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
!       ENDDO
!      ENDDO
!     ENDDO
!     WRITE(6,'(A,I3)') 'THE TRIAL DENSITY MATRIX FOR THE CONVERGED ROOT ',N
!     CALL DUMP9(P_AX,NCGS,CEL1)
!     DEALLOCATE(P_AX)
! ... TO HERE
      IF (IOPTN(9) >= 2) THEN
       WRITE(6,'(A,I3)') 'THE ROOT ',N
       CALL DUMP8(XP,NOV,KVC)
      ENDIF
      S=0.0D0
      DO K=-KVC,MAX(0,KVC-1)
       L=0
       DO MOI=ICORE+1,IOCC
        DO MOA=IOCC+1,IALL(K)-IVIRTCORE
         L=L+1
         S=S+CDABS(XP(L,K))**2
         IF (KVC == 0) THEN
          IF (CDABS(XP(L,K)) > 0.01D0) WRITE(6,'(A,I3,A,I3,A,F15.10)') '         V(',MOA,') - O(',MOI,') ',CDABS(XP(L,K))**2
         ELSE
          IF (CDABS(XP(L,K)) > 0.01D0) &
          WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F15.10)') '         V(',MOA,'[',K,']) - O(',MOI,'[',K,']) ',CDABS(XP(L,K))**2
         ENDIF
        ENDDO
       ENDDO
      ENDDO
      IF (IOPTN(9) >= 2) WRITE(6,'(A,F15.10)') 'NORM OF THE SOLUTION VECTOR =',S
     ENDDO
     DEALLOCATE(A,C,WX,OMEGA,RX,PREV_OMEGA)
     DEALLOCATE(X,XP,PHASE,DEV,LDONE)
     RETURN
    ELSE
     REWIND(38)
     NDISCARDS=0
     DO N=1,NROOTS-NCONVS
      READ(38) RX

      ! CHECK ORTHOGONALITY
      ALLOCATE(UNITC(NTRIALS+1,NTRIALS+1),UNITE(NTRIALS+1),UNITX(NTRIALS+1,NTRIALS+1))
      UNITC(1:NTRIALS+1,1:NTRIALS+1)=DCMPLX(0.0D0,0.0D0)
      DO O=1,NTRIALS+1
       IF (O == NTRIALS+1) THEN
        XP=RX
       ELSE
        REWIND(36)
        DO I=1,O
         READ(36) XP
        ENDDO
       ENDIF
       REWIND(36)
       DO M=1,NTRIALS+1
        IF (M == NTRIALS+1) THEN
         X=RX
        ELSE
         READ(36) X
        ENDIF
        DO K=-KVC,MAX(0,KVC-1)
         DO L=1,NOV
          UNITC(M,O)=UNITC(M,O)+DCONJG(X(L,K))*XP(L,K)
         ENDDO
        ENDDO
       ENDDO
      ENDDO
      CALL HHBS(NTRIALS+1,NTRIALS+1,UNITC,UNITE,UNITX)
      IF (IOPTN(9) >= 2) WRITE(6,'(A,F20.14)') 'SMALLEST METRIC = ',UNITE(1)
      IF (UNITE(1) < 1.0D-9) THEN
       NDISCARDS=NDISCARDS+1
       DEALLOCATE(UNITC,UNITE,UNITX)
       CYCLE
      ENDIF
      DEALLOCATE(UNITC,UNITE,UNITX)

      REWIND(36)
      DO M=1,NTRIALS
       READ(36) X
      ENDDO
      WRITE(36) RX
      IF (IOPTN(9) == 3) THEN
       WRITE(6,'(A,I3)') 'THE NEW TRIAL VECTOR FOR ROOT ',N
       CALL DUMP8(RX,NOV,KVC)
      ENDIF
      NTRIALS=NTRIALS+1
      IF (NTRIALS > MAXTRIALS) CALL PABORT('NUMBER OF TRIAL VECTORS EXCEEDED MAXIMUM ALLOWED VALUE')
     ENDDO
     IF (NDISCARDS > 0) WRITE(6,'(A,I2,A)') &
     '***** WARNING : ',NDISCARDS,' RESIDUAL VECTOR(S) HAVE BEEN DISCARDED TO AVOID LINEAR DEPENDENCE'
     IF (NDISCARDS == NROOTS-NCONVS) THEN
      WRITE(6,'(A)') '----------------------------------------------------------------------------'
      CALL WARNING('THE ALGORITHM FAILED TO EXTEND SUBSPACE')
      IF (IFLAG == 1) THEN
       WRITE(6,'(A)') 'SINGLET ROOTS'
      ELSE IF (IFLAG == 2) THEN
       WRITE(6,'(A)') 'TRIPLET ROOTS'
      ENDIF
      DO N=1,NROOTS
       WRITE(6,'(I9,F10.5,A,F10.5,A)') N,OMEGA(N),' HARTREE (',OMEGA(N)*EV,' EV )'
       REWIND(36)
       XP=DCMPLX(0.0D0,0.0D0)
       DO M=1,NTRIALS
        READ(36) X
        DO K=-KVC,MAX(0,KVC-1)
         DO L=1,NOV
          XP(L,K)=XP(L,K)+A(M,N)*X(L,K)
         ENDDO
        ENDDO
       ENDDO
       S=0.0D0
       DO K=-KVC,MAX(0,KVC-1)
        DO L=1,NOV
         S=S+DCONJG(XP(L,K))*XP(L,K)
        ENDDO
       ENDDO
       IF (S == 0.0D0) CALL PABORT('ZERO SOLUTION VECTOR')
       S=DSQRT(S)
       DO K=-KVC,MAX(0,KVC-1)
        DO L=1,NOV
         XP(L,K)=XP(L,K)/S
        ENDDO
       ENDDO
! DUMP TRIAL DENSITY MATRIX ASSOCIATED WITH THE CONVGD CIS SOLUTION VECTORS, COMMENT OUT FROM HERE ...
!      ALLOCATE(P_AX(NCGS,NCGS,-CEL1:CEL1))
!      DO I=1,NCGS
!       DO J=1,NCGS
!        DO Q=-CEL1,CEL1
!         P_AX(I,J,Q)=DCMPLX(0.0D0,0.0D0)
!         DO K=-KVC,MAX(0,KVC-1)
!          L=0
!          DO MOI=ICORE+1,IOCC
!           DO MOA=IOCC+1,IALL(K)-IVIRTCORE
!            L=L+1
!            P_AX(I,J,Q)=P_AX(I,J,Q)+DCONJG(CO(I,MOI,K))*CO(J,MOA,K)*XP(L,K)*PHASE(K*Q)
!           ENDDO
!          ENDDO
!          IF (L /= NOV) CALL PABORT('AN INTERNAL PROGRAM ERROR IS DETECTED')
!         ENDDO
!         P_AX(I,J,Q)=P_AX(I,J,Q)/DFLOAT(MAX(1,2*KVC))
!        ENDDO
!       ENDDO
!      ENDDO
!      WRITE(6,'(A,I3)') 'THE TRIAL DENSITY MATRIX FOR THE CONVERGED ROOT ',N
!      CALL DUMP9(P_AX,NCGS,CEL1)
!      DEALLOCATE(P_AX)
! ... TO HERE
       IF (IOPTN(9) >= 2) THEN
        WRITE(6,'(A,I3)') 'THE ROOT ',N
        CALL DUMP8(XP,NOV,KVC)
       ENDIF
       S=0.0D0
       DO K=-KVC,MAX(0,KVC-1)
        L=0
        DO MOI=ICORE+1,IOCC
         DO MOA=IOCC+1,IALL(K)-IVIRTCORE
          L=L+1
          S=S+CDABS(XP(L,K))**2
          IF (KVC == 0) THEN
           IF (CDABS(XP(L,K)) > 0.01D0) WRITE(6,'(A,I3,A,I3,A,F15.10)') '         V(',MOA,') - O(',MOI,') ',CDABS(XP(L,K))**2
          ELSE
           IF (CDABS(XP(L,K)) > 0.01D0) &
           WRITE(6,'(A,I3,A,I3,A,I3,A,I3,A,F15.10)') '         V(',MOA,'[',K,']) - O(',MOI,'[',K,']) ',CDABS(XP(L,K))**2
          ENDIF
         ENDDO
        ENDDO
       ENDDO
       IF (IOPTN(9) >= 2) WRITE(6,'(A,F15.10)') 'NORM OF THE SOLUTION VECTOR =',S
      ENDDO
      DEALLOCATE(A,C,WX,OMEGA,RX,PREV_OMEGA)
      DEALLOCATE(X,XP,PHASE,DEV,LDONE)
      RETURN
     ENDIF
     IF (NTRIALS >= NOV*MAX(1,2*KVC)) CALL WARNING('TRIAL VECTORS HAVE ALREADY SPANNED THE WHOLE SPACE')
    ENDIF

    DEALLOCATE(A,C,WX,OMEGA,RX)
    IF (ITR > MAXITR) CALL PABORT('NUMBER OF ITERATIONS EXCEEDED MAXIMUM ALLOWED VALUE')

   ENDDO
END SUBROUTINE
